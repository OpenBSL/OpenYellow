// MIT License

// Copyright (c) 2023-2025 Anton Tsitavets

// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:

// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.

// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

// https://github.com/OpenBSL/OpenYellow

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаписатьИсключение(Знач ОписаниеОшибки) Экспорт
    
    МЗаписи = РегистрыСведений.Исключения.СоздатьМенеджерЗаписи();
    МЗаписи.Период = ТекущаяДатаСеанса();
    МЗаписи.UID = Строка(Новый УникальныйИдентификатор);
    МЗаписи.ОписаниеОшибки = ОписаниеОшибки;
    МЗаписи.Записать(Истина);
    
КонецПроцедуры

Процедура ЗаписатьФайлВРеквизиты(Знач Путь, Знач СтруктураЗаписи) Экспорт
	
	ФайлДвоичные   = Новый ДвоичныеДанные(Путь);
	РеквизитЗаписи = СтруктураЗаписи["Реквизит"];
	РеквизитПути   = СтруктураЗаписи["Путь"];
	
	ОбъектЗаписи = СтруктураЗаписи["Ссылка"].ПолучитьОбъект();
	ОбъектЗаписи[РеквизитЗаписи] = Новый ХранилищеЗначения(ФайлДвоичные);
	
	Если ЗначениеЗаполнено(РеквизитПути) Тогда
		ОбъектЗаписи[РеквизитПути] = Путь;
	КонецЕсли;
	
	ОбъектЗаписи.Записать();

КонецПроцедуры

Процедура ЗаписатьJSONВФайл(Знач Данные, Знач Путь) Экспорт
    
    JSON = Новый ЗаписьJSON();
    
    Настройки   = Новый НастройкиСериализацииJSON();    
    Настройки.ФорматСериализацииДаты = ФорматДатыJSON.ISO;
    
    ПараметрыЗаписиJSON = Новый ПараметрыЗаписиJSON( , Символы.Таб);

    JSON.ОткрытьФайл(Путь, "UTF-8", , ПараметрыЗаписиJSON);
    
    ЗаписатьJSON(JSON, Данные, Настройки);
    JSON.Закрыть();
    
КонецПроцедуры

Процедура УстановитьНастройку(Имя, Значение) Экспорт
    
    СтрокаПодключения = СтрокаСоединенияИнформационнойБазы();
    
    Набор = Справочники.НаборыНастроек.НайтиПоРеквизиту("СтрокаПодключения", СтрокаПодключения);
    
    НаборОбъект = Набор.ПолучитьОбъект();
    НаборОбъект[Имя] = Значение;
    
    НаборОбъект.Записать();
    
КонецПроцедуры

Функция ПолучитьСписокРепозиториев(Знач Запрос, Знач Страница = 1) Экспорт
    
    URL         = Запрос.Запрос + "+fork:true&page=" + Строка(Страница) + "&visibility=public";
    Авторизация = "token " + ПолучитьНастройку("GitHubToken");
    Заголовки   = Новый Соответствие;
    Заголовки.Вставить("Authorization", Авторизация);
    Заголовки.Вставить("Content-Type", "text/html; charset=utf-8");
    
    //@skip-check undefined-variable
    Ответ = OPI_ЗапросыHTTP.Get(URL, , Заголовки);  
    
	Возврат Ответ;
	   
КонецФункции

Функция ПолучитьПоследнийРелиз(Знач Репозиторий) Экспорт
    
    URL = "https://api.github.com/repos/" 
        + Репозиторий.Автор.Наименование 
        + "/" 
        + Репозиторий.Наименование + "/releases/latest";
        
    Авторизация = "token " + ПолучитьНастройку("GithubTokenДополнительный");
    Заголовки   = Новый Соответствие;
    Заголовки.Вставить("Authorization", Авторизация);
    Заголовки.Вставить("Content-Type", "text/html; charset=utf-8");
    
    //@skip-check undefined-variable
    Ответ = OPI_ЗапросыHTTP.Get(URL, , Заголовки);
    
	Возврат Ответ;

КонецФункции

Функция ФорматДата(Знач ДатаСтрока) Экспорт
	
	Возврат Дата(СтрЗаменить(Лев(ДатаСтрока, СтрНайти(ДатаСтрока, "T") - 1), "-", ""));
	
КонецФункции

Функция ТаблицаЗначенийВМассив(ТаблицаЗначений) Экспорт
	
	Массив = Новый Массив();
	СтруктураСтрокой = "";
	НужнаЗапятая = Ложь;
	Для Каждого Колонка Из ТаблицаЗначений.Колонки Цикл
		Если НужнаЗапятая Тогда
			СтруктураСтрокой = СтруктураСтрокой + ",";
		КонецЕсли;
		СтруктураСтрокой = СтруктураСтрокой + Колонка.Имя;
		НужнаЗапятая = Истина;
	КонецЦикла;
	Для Каждого Строка Из ТаблицаЗначений Цикл
		НоваяСтрока = Новый Структура(СтруктураСтрокой);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		Массив.Добавить(НоваяСтрока);
	КонецЦикла;
	Возврат Массив;

КонецФункции

Функция ПолучитьНастройку(Имя) Экспорт
	
	СтрокаПодключения = СтрокаСоединенияИнформационнойБазы();
	
	Набор = Справочники.НаборыНастроек.НайтиПоРеквизиту("СтрокаПодключения", СтрокаПодключения);
	Возврат Набор[Имя];
	
КонецФункции

Функция ПолучитьФайл(Имя) Экспорт

    //@skip-check reading-attribute-from-database
    Возврат Справочники.Файлы[Имя].Файл.Получить();
        
КонецФункции

#КонецОбласти
