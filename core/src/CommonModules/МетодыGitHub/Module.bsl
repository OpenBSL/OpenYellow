// Copyright (c) 2023-2025 Anton Tsitavets

// 
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
// 
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
// 
// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <https://www.gnu.org/licenses/>.

// https://github.com/OpenBSL/OpenYellow

//@skip-check undefined-variable

#Область СлужебныйПрограммныйИнтерфейс

Процедура ОбновитьДанныеОРепозиториях(Знач Страница = 1, Знач Запрос = "") Экспорт
		
    ОпределитьЗапросГитхаб(Запрос);	
    
	Данные = ПолучитьСписокРепозиториев(Запрос, Страница);
	
	Если Данные.Получить("items") <> Неопределено Тогда
		
		ВсегоРепозиториев 	= Данные["total_count"];
		
		Если Страница = 1 Тогда
			ОбъектЗапрос = Запрос.ПолучитьОбъект();
			ОбъектЗапрос.Количество = ВсегоРепозиториев;
			ОбъектЗапрос.Записать();
		КонецЕсли;
		
		Для Каждого Репозиторий Из Данные["items"] Цикл		
            //@skip-check query-in-loop
            ОбработатьЗаписьДанныхРепозитория(Репозиторий);			
		КонецЦикла;
				
		Если 30 * Страница < ВсегоРепозиториев Тогда
			ОбновитьДанныеОРепозиториях(Страница + 1, Запрос);
		Иначе	
			ОбъектЗапрос = Запрос.ПолучитьОбъект();
			ОбъектЗапрос.Использован = Истина;
			ОбъектЗапрос.Записать();
			
            МетодыВыгрзкиДанных.ОбработатьОчередьВыгрузки();
			Возврат;    
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьДанныеРелизов() Экспорт
    
    ТДС = ТекущаяДатаСеанса();
    
    ПолучитьДанныеПоРелизу(ТДС - (60 * 60 * 24 * 10000));
    ПолучитьДанныеПоРелизу(ТДС - (60 * 60 * 24 * 10));
    
КонецПроцедуры

Функция ПолучитьНомерГруппы() Экспорт
    
    Запрос = Новый Запрос;
    Запрос.Текст = 
    "ВЫБРАТЬ
    |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Репозитории.Ссылка) КАК Количество
    |ИЗ
    |	Справочник.Репозитории КАК Репозитории";
    
    РезультатЗапроса = Запрос.Выполнить();
    
    ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
    
    Если ВыборкаДетальныеЗаписи.Следующий() Тогда
        Возврат Цел(ВыборкаДетальныеЗаписи.Количество / 500) + 1;
    Иначе
        Возврат 1;
    КонецЕсли;

КонецФункции
    
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбработатьЗаписьДанныхРепозитория(Знач Репозиторий)
    
    ТекущийРепозиторий = ВернутьРепозиторий(Репозиторий["id"]);
    ЭтоНовый           = Не ЗначениеЗаполнено(ТекущийРепозиторий.Ссылка);
    
    Если Не ЭтоНовый Тогда
        Если ТекущийРепозиторий.ПометкаУдаления Тогда
            Возврат;
        КонецЕсли;
    КонецЕсли;
    
	НачатьЗаписьРепозитория(Репозиторий, ТекущийРепозиторий);	    
	ОбработатьЗаписьАвтора(Репозиторий, ТекущийРепозиторий);
	ЗавершитьЗаписьРепозитория(Репозиторий, ТекущийРепозиторий, ЭтоНовый);
	ОбработатьЗаписьСтатистики(Репозиторий, ТекущийРепозиторий, ЭтоНовый);
	ОбработатьЗаписьОчередиВыгрузки(ТекущийРепозиторий);
	
	OPI_Инструменты.Пауза(5);

КонецПроцедуры

Процедура НачатьЗаписьРепозитория(Знач Репозиторий, ТекущийРепозиторий)
	
	ТекущийРепозиторий.ДатаСоздания = ИнструментарийВызовСервера.ФорматДата(Репозиторий["created_at"]);
    
    Попытка
        ТекущийРепозиторий.ПоследнееОбновление = ИнструментарийВызовСервера.ФорматДата(Репозиторий["pushed_at"]);
    Исключение
        ТекущийРепозиторий.ПоследнееОбновление = ИнструментарийВызовСервера.ФорматДата(Репозиторий["updated_at"]);
    КонецПопытки;
	
	ТекущийРепозиторий.ОтветGithub  = OPI_Инструменты.JSONСтрокой(Репозиторий);
    ТекущийРепозиторий.Наименование = Репозиторий["name"];
    ТекущийРепозиторий.URL          = Репозиторий["html_url"];
    ТекущийРепозиторий.Описание     = Репозиторий["description"];
			
    ТекущийРепозиторий.Язык			= Репозиторий["language"];
    ТекущийРепозиторий.Размер		= Репозиторий["size"];
    ТекущийРепозиторий.ЭтоФорк      = Репозиторий["fork"];
    ТекущийРепозиторий.Лицензия     = ?(Репозиторий["license"] = Неопределено
    , "Нет"
    , Репозиторий["license"]["name"]);
    
    Если ТипЗнч(Репозиторий["topics"]) = Тип("Массив") Тогда
        
        ТекущийРепозиторий.Тэги.Очистить();
        
        Для Каждого Топик Из Репозиторий["topics"] Цикл
            НовыйТэг = ТекущийРепозиторий.Тэги.Добавить();
            НовыйТэг.Тэг = Топик;
        КонецЦикла;
        
	КонецЕсли;	
	
КонецПроцедуры

Процедура ЗавершитьЗаписьРепозитория(Знач Репозиторий, ТекущийРепозиторий, ЭтоНовый)
	
	Если ЭтоНовый Тогда
		
		ТекущийРепозиторий.ДатаДобавления = ТекущаяДатаСеанса();
		ТекущийРепозиторий.Readme         = ПолучитьReadme(ТекущийРепозиторий);
		ТекущийРепозиторий.AIСаммари      = МетодыAI.ПолучитьСаммариРепозитория(ТекущийРепозиторий);
		ТекущийРепозиторий.Записать();
		
        Если Не ТекущийРепозиторий.ЭтоФорк Тогда
            МетодыТелеграм.СоздатьПостНовогоРепозитория(ТекущийРепозиторий);	
        КонецЕсли;
        
	Иначе
		
		Readme = ПолучитьReadme(ТекущийРепозиторий);
		
		Если ЗначениеЗаполнено(Readme) Тогда
			ТекущийРепозиторий.Readme = Readme;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ТекущийРепозиторий.AIСаммари) Тогда
			ТекущийРепозиторий.AIСаммари = МетодыAI.ПолучитьСаммариРепозитория(ТекущийРепозиторий);
		КонецЕсли;
		
		ТекущийРепозиторий.Записать();
		
	КонецЕсли;

КонецПроцедуры

Процедура ОбработатьЗаписьАвтора(Знач Репозиторий, ТекущийРепозиторий)
	
    ДанныеАвтора              = Репозиторий["owner"];
    ТекущийАвтор              = ВернутьАвтора(ДанныеАвтора["id"]);
    ТекущийАвтор.Наименование = ДанныеАвтора["login"];
    ТекущийАвтор.URL          = ДанныеАвтора["html_url"];
    ТекущийАвтор.Аватар       = ДанныеАвтора["avatar_url"];
    
    ТекущийАвтор.Записать();
    
    ТекущийРепозиторий.Автор = ТекущийАвтор.Ссылка;
	

КонецПроцедуры

Процедура ОбработатьЗаписьСтатистики(Знач Репозиторий, ТекущийРепозиторий, ЭтоНовый)
	
	МЗаписи = РегистрыСведений.СтатистикаРепозиториев.СоздатьМенеджерЗаписи();
    МЗаписи.Репозиторий  = ТекущийРепозиторий.Ссылка;
    МЗаписи.Прочитать();
    
    Если Не ЭтоНовый Тогда
        ЗаписатьТренды(МЗаписи.Звезды, Репозиторий["stargazers_count"], ТекущийРепозиторий.Ссылка);
    КонецЕсли;
    
    МЗаписи.Репозиторий  = ТекущийРепозиторий.Ссылка;
    МЗаписи.Период       = ТекущаяДатаСеанса();
    МЗаписи.Звезды       = Репозиторий["stargazers_count"];
    МЗаписи.Подписчики   = Репозиторий["watchers_count"];
    МЗаписи.Форки        = Репозиторий["forks"];
    
    МЗаписи.Записать(Истина);

КонецПроцедуры

Процедура ОбработатьЗаписьОчередиВыгрузки(ТекущийРепозиторий)
	
	МЗаписи = РегистрыСведений.ОчередьВыгрузки.СоздатьМенеджерЗаписи();
	МЗаписи.ОбъектВыгрузки = ТекущийРепозиторий.Автор;
	МЗаписи.Записать(Истина);

	МЗаписи = РегистрыСведений.ОчередьВыгрузки.СоздатьМенеджерЗаписи();
	МЗаписи.ОбъектВыгрузки = ТекущийРепозиторий.Ссылка;
	МЗаписи.Записать(Истина);
	
КонецПроцедуры

Процедура ОпределитьЗапросГитхаб(Запрос)
    
    Если Не ЗначениеЗаполнено(Запрос) Тогда
        
        Запрос = Новый Запрос;
        Запрос.Текст = 
        "ВЫБРАТЬ ПЕРВЫЕ 1
        |	Запросы.Ссылка КАК Ссылка
        |ИЗ
        |	Справочник.Запросы КАК Запросы
        |ГДЕ
        |	НЕ Запросы.Использован
        |	И НЕ Запросы.ПометкаУдаления
        |
        |УПОРЯДОЧИТЬ ПО
        |	Запросы.Код";
        
        РезультатЗапроса = Запрос.Выполнить();
        
        ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
        
        Если ВыборкаДетальныеЗаписи.Следующий() Тогда
            Запрос = ВыборкаДетальныеЗаписи.Ссылка;
        Иначе
            
            ВсеЗапросы = Справочники.Запросы.Выбрать();
            
            Пока ВсеЗапросы.Следующий() Цикл
                
                ОбъектЗапрос = ВсеЗапросы.Ссылка.ПолучитьОбъект();
                ОбъектЗапрос.Использован = Ложь;
                ОбъектЗапрос.Записать();
                
            КонецЦикла;
            
            ОпределитьЗапросГитхаб(Запрос);
            
        КонецЕсли;
        
    КонецЕсли;

КонецПроцедуры

Процедура ЗаписатьТренды(Знач ПрошлоеЗначение, Знач НовоеЗначение, Знач Репозиторий)
    
    Если Не ЗначениеЗаполнено(НовоеЗначение) Тогда
        Возврат;
    КонецЕсли;
    
    Если ПрошлоеЗначение < НовоеЗначение Тогда
        МЗаписи = РегистрыСведений.Тренды.СоздатьМенеджерЗаписи();
        МЗаписи.Репозиторий = Репозиторий;
        МЗаписи.Изменение   = НовоеЗначение - ПрошлоеЗначение;
        МЗаписи.Период      = ТекущаяДатаСеанса();
        МЗаписи.Записать(Истина);
    КонецЕсли;
    
КонецПроцедуры

Процедура ОбновитьДанныеПакетов(Знач Репозиторий, Знач Ассеты, Знач Версия) 
    
    НЗаписи = РегистрыСведений.Пакеты.СоздатьНаборЗаписей(); 
    НЗаписи.Отбор.Репозиторий.Установить(Репозиторий);
    НЗаписи.Отбор.Версия.Установить(Версия);
    НЗаписи.Прочитать();
    НЗаписи.Очистить();
    
    Для Каждого Ассет Из Ассеты Цикл
        
        Если Ассет["state"] = "uploaded" Тогда
            
            ИмяФайла = Ассет["name"];
            
            Если СтрЗаканчиваетсяНа(ИмяФайла, ".epf") Тогда
                Вид = 1;
            ИначеЕсли СтрЗаканчиваетсяНа(ИмяФайла, ".erf") Тогда
                Вид = 2;
            ИначеЕсли СтрЗаканчиваетсяНа(ИмяФайла, ".cfe") Тогда
                Вид = 3;
            Иначе
                Продолжить;
            КонецЕсли;
            
            НовыйФайл = НЗаписи.Добавить();
            НовыйФайл.URL         = Ассет["browser_download_url"];
            НовыйФайл.Размер      = Ассет["size"];
            НовыйФайл.Скачиваний  = Ассет["download_count"];
            НовыйФайл.Файл        = ИмяФайла;
            НовыйФайл.Вид         = Вид;
            НовыйФайл.Репозиторий = Репозиторий;
            НовыйФайл.Версия      = Версия;
            
        КонецЕсли;
        
    КонецЦикла;
    
    НЗаписи.Записать(Истина);
        
КонецПроцедуры

Процедура ПолучитьДанныеПоРелизу(Знач РубежОбновления)
    
    Попытка
        
    ТДС    = ТекущаяДатаСеанса();
    Запрос = Новый Запрос;
    Запрос.Текст = 
        "ВЫБРАТЬ ПЕРВЫЕ 1
        |	Репозитории.Ссылка КАК Репозиторий,
        |	ЕСТЬNULL(РелизыСрезПоследних.Период, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) КАК Период,
        |	ЕСТЬNULL(РелизыСрезПоследних.Версия, """") КАК Версия,
        |	ЕСТЬNULL(РелизыСрезПоследних.ДатаРелиза, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) КАК ДатаРелиза
        |ИЗ
        |	Справочник.Репозитории КАК Репозитории
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Релизы.СрезПоследних КАК РелизыСрезПоследних
        |		ПО (РелизыСрезПоследних.Репозиторий = Репозитории.Ссылка)
        |ГДЕ
        |	Репозитории.ПоследнееОбновление > &ПрошлаяНеделя
        |	И НЕ Репозитории.ПометкаУдаления
        |
        |УПОРЯДОЧИТЬ ПО
        |	Период";
    
    Запрос.УстановитьПараметр("ПрошлаяНеделя", РубежОбновления);
    РезультатЗапроса = Запрос.Выполнить();
    
    ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
    
    Если ВыборкаДетальныеЗаписи.Следующий() Тогда
        
        Репозиторий    = ВыборкаДетальныеЗаписи.Репозиторий;
        ПоследнийРелиз = ПолучитьПоследнийРелиз(Репозиторий);
        ДатаПубликации = ПоследнийРелиз["published_at"];
        Версия         = Строка(ПоследнийРелиз["tag_name"]);
        НоваяВерсия    = Версия <> ВыборкаДетальныеЗаписи.Версия;
        
        МЗаписи = РегистрыСведений.Релизы.СоздатьМенеджерЗаписи();
        МЗаписи.Репозиторий = Репозиторий;
        
        Если НоваяВерсия Тогда
            
            Если ЗначениеЗаполнено(ДатаПубликации)  Тогда
                
                //@skip-check undefined-variable
                OPI_ПреобразованиеТипов.ПолучитьДату(ДатаПубликации); 
                
                МЗаписи.Версия      = Версия;
                МЗаписи.ДатаРелиза  = ДатаПубликации;
                
				Если ДатаПубликации > ТекущаяДатаСеанса() - (60 * 60 * 24 * 7) 
					И ДатаПубликации > ВыборкаДетальныеЗаписи.ДатаРелиза + 86400 * 4 Тогда 
					
                    МетодыТелеграм.СоздатьПостНовогоРелиза(Репозиторий, Версия, ДатаПубликации, ПоследнийРелиз);
                КонецЕсли;         
                
            КонецЕсли; 
                     
        Иначе
            
            Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Период) Тогда
                МЗаписи.Период = ВыборкаДетальныеЗаписи.Период;
                МЗаписи.Прочитать();
            КонецЕсли;
            
        КонецЕсли;
        
        МЗаписи.Период = ТДС;
        МЗаписи.Записать(Истина);
        
        Ассеты = ПоследнийРелиз["assets"];
        Если ЗначениеЗаполнено(Ассеты) Тогда
            ОбновитьДанныеПакетов(Репозиторий, Ассеты, Версия);    
        КонецЕсли;
        
    КонецЕсли;   
    
    Исключение
        ИнструментарийВызовСервера.ЗаписатьИсключение(ОписаниеОшибки());
    КонецПопытки;

КонецПроцедуры

Функция ПолучитьСписокРепозиториев(Знач Запрос, Знач Страница = 1)
    
    URL         = Запрос.Запрос + "+fork:true&page=" + Строка(Страница) + "&visibility=public";
    Авторизация = "token " + ИнструментарийВызовСервера.ПолучитьНастройку("GitHubToken");
    Заголовки   = Новый Соответствие;
    Заголовки.Вставить("Authorization", Авторизация);
    Заголовки.Вставить("Content-Type", "text/html; charset=utf-8");
    
    //@skip-check undefined-variable
    Ответ = OPI_ЗапросыHTTP.Get(URL, , Заголовки);  
    
	Возврат Ответ;
	   
КонецФункции

Функция ПолучитьПоследнийРелиз(Знач Репозиторий)
    
    URL = "https://api.github.com/repos/" 
        + Репозиторий.Автор.Наименование 
        + "/" 
        + Репозиторий.Наименование + "/releases/latest";
        
    Авторизация = "token " + ИнструментарийВызовСервера.ПолучитьНастройку("GithubTokenДополнительный");
    Заголовки   = Новый Соответствие;
    Заголовки.Вставить("Authorization", Авторизация);
    Заголовки.Вставить("Content-Type", "text/html; charset=utf-8");
    
    //@skip-check undefined-variable
    Ответ = OPI_ЗапросыHTTP.Get(URL, , Заголовки);
    
	Возврат Ответ;

КонецФункции

Функция ВернутьРепозиторий(Знач Код)
	
	ТекущийРепозиторий = Справочники.Репозитории.НайтиПоКоду(Код);
	
	Если Не ЗначениеЗаполнено(ТекущийРепозиторий) Тогда
		
		ВозвращаемыйРепозиторий = Справочники.Репозитории.СоздатьЭлемент();
		ВозвращаемыйРепозиторий.Код = Код;
		ВозвращаемыйРепозиторий.Группа = ПолучитьНомерГруппы();
		
	Иначе		
		ВозвращаемыйРепозиторий = ТекущийРепозиторий.ПолучитьОбъект();
	КонецЕсли;
	
	Возврат ВозвращаемыйРепозиторий;
	
КонецФункции

Функция ВернутьАвтора(Знач Код)
	
	ТекущийАвтор = Справочники.Авторы.НайтиПоКоду(Код);
	
	Если Не ЗначениеЗаполнено(ТекущийАвтор) Тогда
		
		ВозвращаемыйАвтор = Справочники.Авторы.СоздатьЭлемент();
		ВозвращаемыйАвтор.Код = Код;
		
	Иначе
		
		ВозвращаемыйАвтор = ТекущийАвтор.ПолучитьОбъект();
	КонецЕсли;
	
	Возврат ВозвращаемыйАвтор;
	
КонецФункции

Функция ПолучитьReadme(Знач Репозиторий)
	
	Попытка
		
		ЗапросAPI = СтрШаблон("https://api.github.com/repos/%1/%2/readme"
		, Репозиторий.Автор.Наименование
		, Репозиторий.Наименование);
		
		Авторизация = "token " + ИнструментарийВызовСервера.ПолучитьНастройку("GitHubToken");
    	Заголовки   = Новый Соответствие;
    	Заголовки.Вставить("Authorization", Авторизация);
		
		Ответ = OPI_ЗапросыHTTP.Get(ЗапросAPI, , Заголовки);
		ПрямойURL = Ответ["download_url"];
		
		Если Не ЗначениеЗаполнено(ПрямойURL) Тогда
			
			Статус = Ответ["status"];
			
			Если ЗначениеЗаполнено(Статус) И Статус = "404" Тогда
				Возврат "";
			КонецЕсли;
			
			ВызватьИсключение OPI_Инструменты.JSONСтрокой(Ответ);
			
		КонецЕсли;
		
		Возврат OPI_ЗапросыHTTP.НовыйЗапрос()
			.Инициализировать(ПрямойURL)
			.ОбработатьЗапрос("GET")
			.ВернутьОтветКакСтроку(, Истина);
			
	Исключение
		ИнструментарийВызовСервера.ЗаписатьИсключение(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат "";
	КонецПопытки;
	
КонецФункции
#КонецОбласти