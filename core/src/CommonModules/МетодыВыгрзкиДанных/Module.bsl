// MIT License

// Copyright (c) 2023-2025 Anton Tsitavets

// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:

// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.

// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

// https://github.com/OpenBSL/OpenYellow

//@skip-check undefined-variable

#Область СлужебныйПрограммныйИнтерфейс

Процедура ВыгрузитьАвторов() Экспорт
	
	СтрокаПодключения = ИнструментарийВызовСервера.ПолучитьНастройку("СтрокаMySQL");
	Таблица           = "authors";
	
	Соединение        = OPI_MySQL.ОткрытьСоединение(СтрокаПодключения);
	
	Если Не OPI_MySQL.ЭтоКоннектор(Соединение) Тогда
		ВызватьИсключение Соединение["error"];
	КонецЕсли;

	ПересоздатьТаблицу(Соединение, Таблица);
	
    Запрос    = Новый Запрос(ПолучитьТекстЗапросаАвторов());    
    Результат = Запрос.Выполнить().Выгрузить();   
    Данные    = ИнструментарийВызовСервера.ТаблицаЗначенийВМассив(Результат);
	
	Для Каждого Позиция Из Данные Цикл
		Позиция.Удалить("Ссылка");	
	КонецЦикла;
	
	Результат = OPI_MySQL.ДобавитьЗаписи(Таблица, Данные, Истина, Соединение);
	
	Если Не Результат["result"] Тогда
		ВызватьИсключение Результат["errors"][0].error;	
	КонецЕсли;

КонецПроцедуры

Процедура ВыгрузитьРепозитории() Экспорт
	
	СтрокаПодключения = ИнструментарийВызовСервера.ПолучитьНастройку("СтрокаMySQL");
	Таблица           = "repos";
	
	Соединение        = OPI_MySQL.ОткрытьСоединение(СтрокаПодключения);
	
	Если Не OPI_MySQL.ЭтоКоннектор(Соединение) Тогда
		ВызватьИсключение Соединение["error"];
	КонецЕсли;

	ПересоздатьТаблицу(Соединение, Таблица);

    Запрос        = Новый Запрос(ПолучитьТекстЗапросаРепозиториев());
    
    Результат = Запрос.Выполнить().Выгрузить();   
    Данные    = ИнструментарийВызовСервера.ТаблицаЗначенийВМассив(Результат);
	
    Для Каждого Позиция Из Данные Цикл
        
        Тэги        = Позиция["tags"];
        МассивТэгов = Новый Массив;
        
        Для Каждого Тэг Из Тэги Цикл
            МассивТэгов.Добавить(Тэг.Тэг);
        КонецЦикла;
        
        Позиция.Вставить("tags", СтрСоединить(МассивТэгов, ","));
		Позиция.Удалить("Ссылка");

    КонецЦикла;
	
	Результат = OPI_MySQL.ДобавитьЗаписи(Таблица, Данные, Истина, Соединение);
	
	Если Не Результат["result"] Тогда
		ВызватьИсключение Результат["errors"][0].error;	
	КонецЕсли;

КонецПроцедуры

Процедура ВыгрузитьНовости() Экспорт
	
	СтрокаПодключения = ИнструментарийВызовСервера.ПолучитьНастройку("СтрокаMySQL");
	Таблица           = "news";
	
	ПересоздатьТаблицу(СтрокаПодключения, Таблица);

    Запрос        = Новый Запрос(ПолучитьТекстЗапросаНовостей());
    
    Результат = Запрос.Выполнить().Выгрузить();   
    Данные    = ИнструментарийВызовСервера.ТаблицаЗначенийВМассив(Результат);
	
	Для Каждого Позиция Из Данные Цикл
		Позиция.Удалить("Ссылка");	
	КонецЦикла;
	
	Результат = OPI_MySQL.ДобавитьЗаписи(Таблица, Данные, Истина, СтрокаПодключения);
	
	Если Не Результат["result"] Тогда
		ВызватьИсключение Результат["errors"][0].error;	
	КонецЕсли;

КонецПроцедуры

Процедура ОбработатьОчередьВыгрузки() Экспорт
	
	Попытка
		ОбновитьДанныеАвторов();
		ОбновитьДанныеРепозиториев();
		ОбновитьДанныеНовостей();
	Исключение
		ИнструментарийВызовСервера.ЗаписатьИсключение(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;

КонецПроцедуры

Процедура ОбновитьДанныеРепозиториев() Экспорт
	
	МассивРепозиториев = ПолучитьОбъектыИзОчереди("Справочник.Репозитории");
	
	Если Не ЗначениеЗаполнено(МассивРепозиториев) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаПодключения = ИнструментарийВызовСервера.ПолучитьНастройку("СтрокаMySQL");
	Соединение        = OPI_MySQL.ОткрытьСоединение(СтрокаПодключения);
	
	Если Не OPI_MySQL.ЭтоКоннектор(Соединение) Тогда
		ВызватьИсключение Соединение["error"];
	КонецЕсли;
	
	ТекстЗапроса = "INSERT INTO repos 
	|(name, url, authorUrl, author, description, createddate, license, stars, forks, pic, lang, size, updateddate, id, isFork, tags) 
	|VALUES (?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?)
	|ON DUPLICATE KEY UPDATE 
  	|id = VALUES(id);";
	
	Запрос = Новый Запрос(ПолучитьТекстЗапросаРепозиториев(Истина));
	Запрос.УстановитьПараметр("МассивРепозиториев",  МассивРепозиториев);
	
	Результат = Запрос.Выполнить().Выгрузить();   
    Данные    = ИнструментарийВызовСервера.ТаблицаЗначенийВМассив(Результат);

    Для Каждого Позиция Из Данные Цикл
		
		ТекущаяСсылка = Позиция["Ссылка"];
        Тэги          = Позиция["tags"];
        МассивТэгов   = Новый Массив;
		
		Для Каждого Тэг Из Тэги Цикл
            МассивТэгов.Добавить(Тэг.Тэг);
        КонецЦикла;
        
        Позиция.Вставить("tags", СтрСоединить(МассивТэгов, ","));
		
		ТекущиеПараметры = Новый Массив;
		ТекущиеПараметры.Добавить(Позиция["name"]);
		ТекущиеПараметры.Добавить(Позиция["url"]);
		ТекущиеПараметры.Добавить(Позиция["authorUrl"]);
		ТекущиеПараметры.Добавить(Позиция["author"]);
		ТекущиеПараметры.Добавить(Позиция["description"]);
		ТекущиеПараметры.Добавить(Позиция["createddate"]);
		ТекущиеПараметры.Добавить(Позиция["license"]);
		ТекущиеПараметры.Добавить(Позиция["stars"]);
		ТекущиеПараметры.Добавить(Позиция["forks"]);
		ТекущиеПараметры.Добавить(Позиция["pic"]);
		ТекущиеПараметры.Добавить(Позиция["lang"]);
		ТекущиеПараметры.Добавить(Позиция["size"]);
		ТекущиеПараметры.Добавить(Позиция["updateddate"]);
		ТекущиеПараметры.Добавить(Позиция["id"]);
		ТекущиеПараметры.Добавить(Позиция["isFork"]);
		ТекущиеПараметры.Добавить(Позиция["tags"]);
		
		РезультатЗапроса = OPI_MySQL.ВыполнитьЗапросSQL(ТекстЗапроса, ТекущиеПараметры, , Соединение);
		
		МЗ = РегистрыСведений.ОчередьВыгрузки.СоздатьМенеджерЗаписи();
		МЗ.ОбъектВыгрузки = ТекущаяСсылка;
		МЗ.Прочитать();
		
		Если Не РезультатЗапроса["result"] Тогда
			МЗ.Ошибка = РезультатЗапроса["error"];
			МЗ.Записать(Истина);
		Иначе
			МЗ.Удалить();	
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

Процедура ОбновитьДанныеАвторов() Экспорт
	
	МассивАвторов = ПолучитьОбъектыИзОчереди("Справочник.Авторы");
	
	Если Не ЗначениеЗаполнено(МассивАвторов) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаПодключения = ИнструментарийВызовСервера.ПолучитьНастройку("СтрокаMySQL");
	Соединение        = OPI_MySQL.ОткрытьСоединение(СтрокаПодключения);
	
	Если Не OPI_MySQL.ЭтоКоннектор(Соединение) Тогда
		ВызватьИсключение Соединение["error"];
	КонецЕсли;
	
	ТекстЗапроса = "INSERT INTO authors (name, url, pic, repos, stars) VALUES (?, ?, ?, ?, ?)
	|ON DUPLICATE KEY UPDATE 
  	|name = VALUES(name);";
	
	Запрос = Новый Запрос(ПолучитьТекстЗапросаАвторов(Истина));
	Запрос.УстановитьПараметр("МассивАвторов",  МассивАвторов);
	
	Результат = Запрос.Выполнить().Выгрузить();   
    Данные    = ИнструментарийВызовСервера.ТаблицаЗначенийВМассив(Результат);

    Для Каждого Позиция Из Данные Цикл
		
		ТекущаяСсылка = Позиция["Ссылка"];			
		
		ТекущиеПараметры = Новый Массив;
		ТекущиеПараметры.Добавить(Позиция["name"]);
		ТекущиеПараметры.Добавить(Позиция["url"]);
		ТекущиеПараметры.Добавить(Позиция["pic"]);
		ТекущиеПараметры.Добавить(Позиция["repos"]);
		ТекущиеПараметры.Добавить(Позиция["stars"]);
		
		РезультатЗапроса = OPI_MySQL.ВыполнитьЗапросSQL(ТекстЗапроса, ТекущиеПараметры, , Соединение);
		
		МЗ = РегистрыСведений.ОчередьВыгрузки.СоздатьМенеджерЗаписи();
		МЗ.ОбъектВыгрузки = ТекущаяСсылка;
		МЗ.Прочитать();
		
		Если Не РезультатЗапроса["result"] Тогда
			МЗ.Ошибка = РезультатЗапроса["error"];
			МЗ.Записать(Истина);
		Иначе
			МЗ.Удалить();	
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

Процедура ОбновитьДанныеНовостей() Экспорт
	
	МассивПостов = ПолучитьОбъектыИзОчереди("Справочник.Посты");
	
	Если Не ЗначениеЗаполнено(МассивПостов) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаПодключения = ИнструментарийВызовСервера.ПолучитьНастройку("СтрокаMySQL");
	Соединение        = OPI_MySQL.ОткрытьСоединение(СтрокаПодключения);
	
	Если Не OPI_MySQL.ЭтоКоннектор(Соединение) Тогда
		ВызватьИсключение Соединение["error"];
	КонецЕсли;
	
	ТекстЗапроса = "INSERT INTO news (id, link, text, icon, title, created_at) VALUES (?, ?, ?, ?, ?, ?)
	|ON DUPLICATE KEY UPDATE 
  	|id = VALUES(id);";
	
	Запрос = Новый Запрос(ПолучитьТекстЗапросаНовостей(Истина));
	Запрос.УстановитьПараметр("МассивПостов",  МассивПостов);
	
	Результат = Запрос.Выполнить().Выгрузить();   
    Данные    = ИнструментарийВызовСервера.ТаблицаЗначенийВМассив(Результат);
	
    Для Каждого Позиция Из Данные Цикл
		
		ТекущаяСсылка = Позиция["Ссылка"];			
		
		ТекущиеПараметры = Новый Массив;
		ТекущиеПараметры.Добавить(Позиция["id"]);
		ТекущиеПараметры.Добавить(Позиция["link"]);
		ТекущиеПараметры.Добавить(Позиция["text"]);
		ТекущиеПараметры.Добавить(Позиция["icon"]);
		ТекущиеПараметры.Добавить(Позиция["title"]);
		ТекущиеПараметры.Добавить(Позиция["created_at"]);
		
		РезультатЗапроса = OPI_MySQL.ВыполнитьЗапросSQL(ТекстЗапроса, ТекущиеПараметры, , Соединение);
		
		МЗ = РегистрыСведений.ОчередьВыгрузки.СоздатьМенеджерЗаписи();
		МЗ.ОбъектВыгрузки = ТекущаяСсылка;
		МЗ.Прочитать();
		
		Если Не РезультатЗапроса["result"] Тогда
			МЗ.Ошибка = РезультатЗапроса["error"];
			МЗ.Записать(Истина);
		Иначе
			МЗ.Удалить();	
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

Процедура ОбновитьJSONДанныеСайта(Знач Повтор = 0) Экспорт
    
    Попытка
        
        ВыгрузитьИндексы();
		ВыгрузитьЗначки();
        
        ЛокальныйКаталог = ИнструментарийВызовСервера.ПолучитьНастройку("ПапкаДанных");
        УдаленныйКаталог = "/data/";
        Токен            = ИнструментарийВызовСервера.ПолучитьНастройку("ТокенNeocities");
		
		ВыгрузитьПодкаталог(Токен, ЛокальныйКаталог + "badges", УдаленныйКаталог + "badges");
		ВыгрузитьПодкаталог(Токен, ЛокальныйКаталог + "ypm_index", УдаленныйКаталог + "ypm_index");
        
    Исключение
        
        ИнструментарийВызовСервера.ЗаписатьИсключение(ОписаниеОшибки());
        
        Если Повтор < 1 Тогда      
            Повтор = Повтор + 1;
            ОбновитьJSONДанныеСайта(Повтор);    
        Иначе
            ИнструментарийВызовСервера.ЗаписатьИсключение("Данные сайта не были обновлены!");
        КонецЕсли;
        
    КонецПопытки;
        
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СУБД

Процедура ПересоздатьТаблицу(Соединение, ИмяТаблицы)
		
	Если ИмяТаблицы = "authors" Тогда
		
		OPI_MySQL.ВыполнитьЗапросSQL("ALTER TABLE repos DROP FOREIGN KEY repos_ibfk_1;", , , Соединение);
		OPI_MySQL.УдалитьТаблицу(ИмяТаблицы, Соединение);
		
		СтруктураКолонок = Новый Соответствие;
		
		СтруктураКолонок.Вставить("name" , "VARCHAR(255) PRIMARY KEY");
		СтруктураКолонок.Вставить("url"  , "VARCHAR(500)");
		СтруктураКолонок.Вставить("pic"  , "VARCHAR(500)");
		СтруктураКолонок.Вставить("repos", "SMALLINT UNSIGNED");
		СтруктураКолонок.Вставить("stars", "MEDIUMINT UNSIGNED");
		
		Результат = OPI_MySQL.СоздатьТаблицу("authors", СтруктураКолонок, Соединение);
		
		Если Не Результат["result"] Тогда
			ВызватьИсключение Результат["error"];	
		КонецЕсли;
		
		Результат = OPI_MySQL.ВыполнитьЗапросSQL(
			"ALTER TABLE authors CONVERT TO CHARACTER SET utf8mb4 COLLATE utf8mb4_unicode_ci;", 
			, , 
			Соединение
		);	
		
		Если Не Результат["result"] Тогда
			ВызватьИсключение Результат["error"];	
		КонецЕсли;

	КонецЕсли;

	Если ИмяТаблицы = "repos" Тогда
		
		OPI_MySQL.УдалитьТаблицу(ИмяТаблицы, Соединение);
		
		СтруктураКолонок = Новый Соответствие;
		СтруктураКолонок.Вставить("id"         , "INT UNSIGNED PRIMARY KEY");
		СтруктураКолонок.Вставить("author"     , "VARCHAR(255)");
		СтруктураКолонок.Вставить("name"       , "VARCHAR(255)");
		СтруктураКолонок.Вставить("url"        , "VARCHAR(500)");
		СтруктураКолонок.Вставить("authorUrl"  , "VARCHAR(500)");	
		СтруктураКолонок.Вставить("description", "TEXT");
		СтруктураКолонок.Вставить("createddate", "DATE");
		СтруктураКолонок.Вставить("license"    , "VARCHAR(255)");
		СтруктураКолонок.Вставить("stars"      , "MEDIUMINT UNSIGNED");
		СтруктураКолонок.Вставить("forks"      , "SMALLINT UNSIGNED");
		СтруктураКолонок.Вставить("pic"        , "VARCHAR(500)");
		СтруктураКолонок.Вставить("lang"       , "VARCHAR(100)");
		СтруктураКолонок.Вставить("size"       , "INT UNSIGNED");
		СтруктураКолонок.Вставить("isFork"     , "TINYINT UNSIGNED");
		СтруктураКолонок.Вставить("updateddate", "DATE");
		СтруктураКолонок.Вставить("tags"       , "VARCHAR(700)");
		
		Результат = OPI_MySQL.СоздатьТаблицу("repos", СтруктураКолонок, Соединение);
		
		Если Не Результат["result"] Тогда
			ВызватьИсключение Результат["error"];	
		КонецЕсли;
		
		Результат = OPI_MySQL.ВыполнитьЗапросSQL(
			"ALTER TABLE repos CONVERT TO CHARACTER SET utf8mb4 COLLATE utf8mb4_unicode_ci;", 
			, , 
			Соединение
		);	
		
		Если Не Результат["result"] Тогда
			ВызватьИсключение Результат["error"];	
		КонецЕсли;

		Результат = OPI_MySQL.ВыполнитьЗапросSQL(
			"ALTER TABLE repos ADD FOREIGN KEY (author) REFERENCES authors(name) ON DELETE CASCADE;", , , 
			Соединение
		);
		
		Если Не Результат["result"] Тогда
			ВызватьИсключение Результат["error"];	
		КонецЕсли;
	
	КонецЕсли;
	
	Если ИмяТаблицы = "news" Тогда
		
		OPI_MySQL.УдалитьТаблицу(ИмяТаблицы, Соединение);
		
		СтруктураКолонок = Новый Соответствие;
		СтруктураКолонок.Вставить("id"         , "INT UNSIGNED PRIMARY KEY");
		СтруктураКолонок.Вставить("created_at" , "DATETIME");
		СтруктураКолонок.Вставить("title"      , "VARCHAR(400)");
		СтруктураКолонок.Вставить("text"       , "TEXT");
		СтруктураКолонок.Вставить("icon"       , "VARCHAR(500)");	
		СтруктураКолонок.Вставить("link"       , "VARCHAR(500)");
		
		Результат = OPI_MySQL.СоздатьТаблицу("news", СтруктураКолонок, Соединение);
		
		Если Не Результат["result"] Тогда
			ВызватьИсключение Результат["error"];	
		КонецЕсли;
		
		Результат = OPI_MySQL.ВыполнитьЗапросSQL(
			"ALTER TABLE news CONVERT TO CHARACTER SET utf8mb4 COLLATE utf8mb4_unicode_ci;", 
			, , 
			Соединение
		);	
		
		Если Не Результат["result"] Тогда
			ВызватьИсключение Результат["error"];	
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьТекстЗапросаРепозиториев(ОтборПоМассиву = Ложь)
    
    Текст = "ВЫБРАТЬ
            |	Репозитории.Наименование КАК name,
            |	Репозитории.URL КАК url,
            |	Репозитории.Автор.URL КАК authorUrl,
            |	Репозитории.Автор.Наименование КАК author,
            |	Репозитории.Описание КАК description,
            |	Репозитории.ДатаСоздания КАК createddate,
            |	Репозитории.Лицензия КАК license,
            |	СтатистикаРепозиториевСрезПоследних.Звезды КАК stars,
            |	СтатистикаРепозиториевСрезПоследних.Форки КАК forks,
            |	Репозитории.Автор.Аватар КАК pic,
            |	Репозитории.Язык КАК lang,
            |	Репозитории.Размер КАК size,
            |	Репозитории.ПоследнееОбновление КАК updateddate,
            |	Репозитории.Код КАК id,
            |	ВЫБОР
            |		КОГДА Репозитории.ЭтоФорк
            |			ТОГДА 1
            |		ИНАЧЕ 0
            |	КОНЕЦ КАК isFork,
            |	Репозитории.Тэги.(
            |		Тэг КАК Тэг
            |	) КАК tags,
            |	Репозитории.Ссылка КАК Ссылка
            |ИЗ
            |	Справочник.Репозитории КАК Репозитории
            |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатистикаРепозиториев КАК СтатистикаРепозиториевСрезПоследних
            |		ПО (СтатистикаРепозиториевСрезПоследних.Репозиторий = Репозитории.Ссылка)
            |ГДЕ
            |	НЕ Репозитории.ПометкаУдаления
            |	И НЕ СтатистикаРепозиториевСрезПоследних.Звезды ЕСТЬ NULL
            |	И НЕ Репозитории.Автор.ПометкаУдаления";
	
	Если ОтборПоМассиву Тогда
		Текст = Текст + Символы.ПС + "	И Репозитории.Ссылка В (&МассивРепозиториев)";
	КонецЕсли;
	
	Возврат Текст;
    
КонецФункции

Функция ПолучитьТекстЗапросаАвторов(ОтборПоМассиву = Ложь)
	
	Текст = "ВЫБРАТЬ
	        |	Репозитории.Автор.Наименование КАК Автор,
	        |	Репозитории.Автор.URL КАК URL,
	        |	Репозитории.Автор.Аватар КАК Аватар,
	        |	ЕСТЬNULL(КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Репозитории.Ссылка), 0) КАК Репозиториев,
	        |	ЕСТЬNULL(СУММА(СтатистикаРепозиториев.Звезды), 0) КАК Звезд,
	        |	Репозитории.Автор КАК Ссылка
	        |ПОМЕСТИТЬ Основной
	        |ИЗ
	        |	Справочник.Репозитории КАК Репозитории
	        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатистикаРепозиториев КАК СтатистикаРепозиториев
	        |		ПО (СтатистикаРепозиториев.Репозиторий = Репозитории.Ссылка)
	        |ГДЕ
	        |	НЕ Репозитории.ПометкаУдаления
	        |	И НЕ Репозитории.Автор.ПометкаУдаления
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	Репозитории.Автор.Наименование,
	        |	Репозитории.Автор.URL,
	        |	Репозитории.Автор.Аватар,
	        |	Репозитории.Автор
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	Основной.Автор КАК name,
	        |	Основной.URL КАК url,
	        |	Основной.Аватар КАК pic,
	        |	Основной.Репозиториев КАК repos,
	        |	Основной.Звезд КАК stars,
	        |	Основной.Ссылка КАК Ссылка
	        |ИЗ
	        |	Основной КАК Основной
	        |ГДЕ
	        |	Основной.Репозиториев > 0";
	
	Если ОтборПоМассиву Тогда
		Текст = Текст + Символы.ПС + "	И Основной.Ссылка В (&МассивАвторов)";
	КонецЕсли;
	
	Возврат Текст;
		
КонецФункции
		
Функция ПолучитьТекстЗапросаНовостей(ОтборПоМассиву = Ложь)
	
	Текст = "ВЫБРАТЬ
	        |	Посты.Новость КАК text,
	        |	Посты.Заголовок КАК title,
	        |	Посты.URL КАК link,
	        |	ЕСТЬNULL(Посты.Репозиторий.Автор.Аватар, """") КАК icon,
	        |	Посты.Дата КАК created_at,
	        |	Посты.Код КАК id,
	        |	Посты.Ссылка КАК Ссылка
	        |ИЗ
	        |	Справочник.Посты КАК Посты
	        |ГДЕ
	        |	(ВЫРАЗИТЬ(Посты.Новость КАК СТРОКА(300))) <> """"
	        |	И (ВЫРАЗИТЬ(Посты.Заголовок КАК СТРОКА(300))) <> """"
	        |	И Посты.Статус = ЗНАЧЕНИЕ(Перечисление.СогласованиеПостов.Опубликован)";
	
	Если ОтборПоМассиву Тогда
		Текст = Текст + Символы.ПС + "	И Посты.Ссылка В (&МассивПостов)";
	КонецЕсли;
	
	Возврат Текст;

КонецФункции

Функция ПолучитьОбъектыИзОчереди(ТипОбъекта)
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрШаблон( 
		"ВЫБРАТЬ
		|	ОчередьВыгрузки.ОбъектВыгрузки КАК ОбъектВыгрузки
		|ИЗ
		|	РегистрСведений.ОчередьВыгрузки КАК ОчередьВыгрузки
		|ГДЕ
		|	ОчередьВыгрузки.Ошибка = """"
		|	И ТИПЗНАЧЕНИЯ(ОчередьВыгрузки.ОбъектВыгрузки) = ТИП(%1)", ТипОбъекта);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	МассивОбъектов         = Новый Массив;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		МассивОбъектов.Добавить(ВыборкаДетальныеЗаписи.ОбъектВыгрузки);
	КонецЦикла;
	
	Возврат МассивОбъектов;

КонецФункции

#КонецОбласти

#Область Статика

Процедура ВыгрузитьИндексы()
    
    ИмяКаталога     = "ypm_index/";
    КаталогИндексов = ИнструментарийВызовСервера.ПолучитьНастройку("ПапкаДанных") + ИмяКаталога;
    ПоследняяГруппа = МетодыGitHub.ПолучитьНомерГруппы();
    МассивИндексов  = Новый Массив;
    АдресСайта      = ИнструментарийВызовСервера.ПолучитьНастройку("АдресСайта");
    ТекущаяДата     = XMLСтрока(НачалоЧаса(ТекущаяДатаСеанса()));
    ВерсияYPM       = Константы.ВерсияYPM.Получить();
    
    Для Н = 1 По ПоследняяГруппа Цикл
        
        ИмяФайла        = Строка(Н);
        ИмяСРасширением = ИмяФайла + ".json";
        
        Путь          = КаталогИндексов + ИмяСРасширением;
        //@skip-check query-in-loop
        ДанныеИндекса = ПолучитьИндексМенеджера(Н);
        ИнструментарийВызовСервера.ЗаписатьJSONВФайл(ДанныеИндекса, Путь);
        МассивИндексов.Добавить(АдресСайта + "data/" + ИмяКаталога + ИмяСРасширением);
        
    КонецЦикла;
    
    СтруктураИндекса = Новый Структура;
    СтруктураИндекса.Вставить("updatedAt"        , ТекущаяДата);
    СтруктураИндекса.Вставить("ypmVersion"       , ВерсияYPM.Наименование); 
    СтруктураИндекса.Вставить("updateDescription", ВерсияYPM.Описание);
    СтруктураИндекса.Вставить("fileUrl"          , ВерсияYPM.URLФайла);
    СтруктураИндекса.Вставить("chunks"           , МассивИндексов);
    
    ИнструментарийВызовСервера.ЗаписатьJSONВФайл(СтруктураИндекса, КаталогИндексов + "index.json");
       
КонецПроцедуры

Процедура ВыгрузитьЗначки()
    
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 500
		|	СтатистикаРепозиториев.Репозиторий КАК Репозиторий,
		|	СтатистикаРепозиториев.Звезды КАК Звезды,
		|	СтатистикаРепозиториев.Подписчики КАК Подписчики,
		|	СтатистикаРепозиториев.Форки КАК Форки,
		|	СтатистикаРепозиториев.ПрошлоеМесто КАК ПрошлоеМесто,
		|	СтатистикаРепозиториев.Репозиторий.Код КАК Код,
		|	СтатистикаРепозиториев.Репозиторий.Группа КАК Группа
		|ИЗ
		|	РегистрСведений.СтатистикаРепозиториев КАК СтатистикаРепозиториев
		|ГДЕ
		|	НЕ СтатистикаРепозиториев.Репозиторий.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	Звезды УБЫВ,
		|	Форки УБЫВ,
		|	СтатистикаРепозиториев.Репозиторий.ДатаСоздания";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Счетчик = 1;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
    	ЗаписатьДанныеЗначка(Счетчик, ВыборкаДетальныеЗаписи.Код, ВыборкаДетальныеЗаписи.Группа);		
		Счетчик = Счетчик + 1;
		
    КонецЦикла;
        
КонецПроцедуры

Процедура ЗаписатьДанныеЗначка(Знач Место, Знач Код, Знач Группа)
    
    Код     	= OPI_Инструменты.ЧислоВСтроку(Код);
	Место    	= OPI_Инструменты.ЧислоВСтроку(Место);
	Группа      = OPI_Инструменты.ЧислоВСтроку(Группа);
    ПапкаДанных	= ИнструментарийВызовСервера.ПолучитьНастройку("ПапкаДанных");
    
    МассивПутей = Новый Массив;
    МассивПутей.Добавить(ПапкаДанных);
		
	Лого = Константы.Лого.Получить();

	СтруктураJSON = Новый Структура;
	СтруктураJSON.Вставить("schemaVersion", 1);
	СтруктураJSON.Вставить("label"        , "OpenYellow");
	СтруктураJSON.Вставить("message"      , "#" + Место);
	СтруктураJSON.Вставить("color"        , "yellow");
	СтруктураJSON.Вставить("logoSvg"      , Лого);
    
    Поток = Новый ПотокВПамяти();
      
    JSON = Новый ЗаписьJSON;
    JSON.ОткрытьПоток(Поток, "UTF-8", Ложь);
    ЗаписатьJSON(JSON, СтруктураJSON);
    JSON.Закрыть();
    
    Двоичные = Поток.ЗакрытьИПолучитьДвоичныеДанные();
    
    Для Каждого Папка Из МассивПутей Цикл
        
        ПутьГруппы		= Папка + "badges\" + Группа;
        КаталогГруппы 	= Новый Файл(ПутьГруппы);
        
        Если Не КаталогГруппы.Существует() Тогда
            СоздатьКаталог(ПутьГруппы);
        КонецЕсли;
        
        Путь  = ПутьГруппы + "\" + Код + ".json";       
        Двоичные.Записать(Путь); 
        
    КонецЦикла;

КонецПроцедуры

Процедура ВыгрузитьПодкаталог(Знач Токен, Знач ЛокальныйКаталог, Знач УдаленныйКаталог)
	
	Выгрузка  = OPI_Neocities.СинхронизироватьКаталоги(Токен, ЛокальныйКаталог, УдаленныйКаталог);
	Ошибки    = Выгрузка["errors"];
	
	Если Ошибки > 0 Тогда
	    
	    Ответ = OPI_Инструменты.JSONСтрокой(Выгрузка);
		ИнструментарийВызовСервера.ЗаписатьИсключение(Ответ);
        ВызватьИсключение Ответ;
         
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьИндексМенеджера(Знач НомерГруппы)
    
    Запрос = Новый Запрос;
    Запрос.Текст = 
        "ВЫБРАТЬ РАЗЛИЧНЫЕ
        |   Репозитории.Наименование КАК Наименование,
        |   Репозитории.Автор.Наименование КАК Автор,
        |   Репозитории.Автор.URL КАК URLАвтора,
        |   ВЫРАЗИТЬ(Репозитории.Описание КАК СТРОКА(500)) КАК Описание,
        |   Репозитории.URL КАК URL,
        |   Репозитории.Лицензия КАК Лицензия,
        |   ЕСТЬNULL(СтатистикаРепозиториев.Звезды, 0) КАК Звезды,
        |   Репозитории.Ссылка КАК Ссылка,
        |   Репозитории.Код КАК Код
        |ПОМЕСТИТЬ ДанныеРепозиториев
        |ИЗ
        |   РегистрСведений.Пакеты КАК Пакеты
        |       ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Репозитории КАК Репозитории
        |           ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатистикаРепозиториев КАК СтатистикаРепозиториев
        |           ПО (СтатистикаРепозиториев.Репозиторий = Репозитории.Ссылка)
        |       ПО Пакеты.Репозиторий = Репозитории.Ссылка
        |ГДЕ
        |   Репозитории.Группа = &Группа
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |   ДанныеРепозиториев.Наименование КАК name,
        |   ДанныеРепозиториев.Описание КАК description,
        |   ДанныеРепозиториев.Автор КАК authorName,
        |   ДанныеРепозиториев.URLАвтора КАК authorURL,
        |   ДанныеРепозиториев.URL КАК URL,
        |   ДанныеРепозиториев.Лицензия КАК license,
        |   ДанныеРепозиториев.Звезды КАК stars,
        |   Пакеты.Версия КАК version,
        |   Пакеты.Файл КАК file,
        |   Пакеты.URL КАК fileURL,
        |   Пакеты.Размер КАК size,
        |   Пакеты.Скачиваний КАК downloads,
        |   Пакеты.Вид КАК type,
        |   ДанныеРепозиториев.Ссылка КАК object,
        |   ДанныеРепозиториев.Код КАК id,
        |   Релизы.ДатаРелиза КАК date
        |ПОМЕСТИТЬ Финал
        |ИЗ
        |   ДанныеРепозиториев КАК ДанныеРепозиториев
        |       ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Пакеты КАК Пакеты
        |           ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Релизы КАК Релизы
        |           ПО Пакеты.Репозиторий = Релизы.Репозиторий
        |               И Пакеты.Версия = Релизы.Версия
        |       ПО ДанныеРепозиториев.Ссылка = Пакеты.Репозиторий
        |
        |СГРУППИРОВАТЬ ПО
        |   Пакеты.Файл,
        |   Пакеты.URL,
        |   ДанныеРепозиториев.URLАвтора,
        |   ДанныеРепозиториев.Автор,
        |   ДанныеРепозиториев.URL,
        |   Пакеты.Версия,
        |   ДанныеРепозиториев.Описание,
        |   ДанныеРепозиториев.Лицензия,
        |   ДанныеРепозиториев.Наименование,
        |   ДанныеРепозиториев.Звезды,
        |   Пакеты.Размер,
        |   Пакеты.Скачиваний,
        |   Пакеты.Вид,
        |   ДанныеРепозиториев.Ссылка,
        |   ДанныеРепозиториев.Код,
        |   Релизы.ДатаРелиза
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |   Финал.name КАК name,
        |   Финал.description КАК description,
        |   Финал.authorName КАК authorName,
        |   Финал.authorURL КАК authorURL,
        |   Финал.URL КАК URL,
        |   Финал.license КАК license,
        |   Финал.stars КАК stars,
        |   Финал.version КАК version,
        |   Финал.file КАК file,
        |   Финал.fileURL КАК fileURL,
        |   Финал.size КАК size,
        |   Финал.downloads КАК downloads,
        |   Финал.type КАК type,
        |   Финал.object КАК object,
        |   Финал.id КАК id,
        |   МАКСИМУМ(ЕСТЬNULL(Финал.date, ДАТАВРЕМЯ(1, 1, 1))) КАК date
        |ИЗ
        |   Финал КАК Финал
        |
        |СГРУППИРОВАТЬ ПО
        |   Финал.authorURL,
        |   Финал.description,
        |   Финал.name,
        |   Финал.authorName,
        |   Финал.URL,
        |   Финал.license,
        |   Финал.version,
        |   Финал.file,
        |   Финал.fileURL,
        |   Финал.stars,
        |   Финал.size,
        |   Финал.downloads,
        |   Финал.type,
        |   Финал.object,
        |   Финал.id
        |ИТОГИ
        |   МАКСИМУМ(name),
        |   МАКСИМУМ(description),
        |   МАКСИМУМ(authorName),
        |   МАКСИМУМ(authorURL),
        |   МАКСИМУМ(URL),
        |   МАКСИМУМ(license),
        |   МАКСИМУМ(stars),
        |   МАКСИМУМ(file),
        |   МАКСИМУМ(size),
        |   МАКСИМУМ(downloads),
        |   МАКСИМУМ(type),
        |   МАКСИМУМ(id)
        |ПО
        |   object,
        |   version";
    
    Запрос.УстановитьПараметр("Группа", НомерГруппы);
    РезультатЗапроса = Запрос.Выполнить();
    МассивИндекса    = Новый Массив;
    ПоляРепозитория  = "id,name,description,authorName,authorURL,URL,license,stars";
    ПоляФайла        = "file,fileURL,size,downloads,type,date";
    
    ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
    
    Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

        СтруктураТекущего = Новый Структура(ПоляРепозитория);
        ЗаполнитьЗначенияСвойств(СтруктураТекущего, ВыборкаДетальныеЗаписи);
        
        МассивВерсий  = Новый Массив;       
        ВыборкаВерсий = ВыборкаДетальныеЗаписи.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
        
        Пока ВыборкаВерсий.Следующий() Цикл
            
            МассивФайлов    = Новый Массив;
            СтруктураВерсии = Новый Структура;
            СтруктураВерсии.Вставить("version", ВыборкаВерсий.version);
            
            ВыборкаФайлов = ВыборкаВерсий.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
            
            Пока ВыборкаФайлов.Следующий() Цикл
                
                СтруктураФайла = Новый Структура(ПоляФайла);
                ЗаполнитьЗначенияСвойств(СтруктураФайла, ВыборкаФайлов);
                МассивФайлов.Добавить(СтруктураФайла);
                
            КонецЦикла;
            
            СтруктураВерсии.Вставить("files", МассивФайлов);
            
            МассивВерсий.Добавить(СтруктураВерсии);
            
        КонецЦикла;
        
        СтруктураТекущего.Вставить("versions", МассивВерсий);
        
        МассивИндекса.Добавить(СтруктураТекущего);
        
    КонецЦикла;  
    
    Возврат МассивИндекса;
    
КонецФункции

#КонецОбласти

#КонецОбласти