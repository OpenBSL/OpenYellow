// Copyright (c) 2023-2025 Anton Tsitavets

// 
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
// 
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
// 
// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <https://www.gnu.org/licenses/>.

// https://github.com/OpenBSL/OpenYellow

//@skip-check undefined-variable
//@skip-check bsl-legacy-check-string-literal

#Область СлужебныйПрограммныйИнтерфейс

Процедура ОпубликоватьОчереднойПост() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Посты.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Посты КАК Посты
		|ГДЕ
		|	Посты.Статус = ЗНАЧЕНИЕ(Перечисление.СогласованиеПостов.Новый)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		
		ПостОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
		
		Попытка
			
			Токен = ИнструментарийВызовСервера.ПолучитьНастройку("ТокенТелеграм");
		    Чат   = ИнструментарийВызовСервера.ПолучитьНастройку("ЧатТелеграм");
			
			Результат = OPI_Telegram.ОтправитьТекстовоеСообщение(Токен, Чат, ПостОбъект.Текст, ПостОбъект.Клавиатура);
			
			Если Не Результат["ok"] Тогда
				ВызватьИсключение Результат["description"];
			Иначе
				ПостОбъект.IDПоста = Результат["result"]["message_id"];
			КонецЕсли;

			ПостОбъект.Статус = ПредопределенноеЗначение("Перечисление.СогласованиеПостов.Опубликован");
			
		Исключение
			ПостОбъект.Ошибка = ОписаниеОшибки();
			ПостОбъект.Статус = ПредопределенноеЗначение("Перечисление.СогласованиеПостов.Ошибка");
		КонецПопытки;
		
		ПостОбъект.Записать();
		
	КонецЕсли;

КонецПроцедуры

Процедура СоздатьПостНовогоРепозитория(Репозиторий) Экспорт
    
	Попытка
			
		Тексты  = Новый Массив;
		Шаблоны = Новый Массив;
		Шаблоны.Добавить(ПолучитьОбщийМакет("СообщениеНовогоРепозитория").ПолучитьТекст());
		Шаблоны.Добавить(ПолучитьОбщийМакет("НовостьНовогоРепозитория").ПолучитьТекст());
		
		Для Каждого Шаблон Из Шаблоны Цикл
			
		    Текст = СтрШаблон(Шаблон
		        , Репозиторий.Автор.Наименование
		        , Репозиторий.Наименование
		        , Репозиторий.URL
		        , ?(ЗначениеЗаполнено(Репозиторий.Язык), Репозиторий.Язык, "Другое")
				, ?(Репозиторий.ЭтоФорк, "Да", "Нет")
		        , ?(ЗначениеЗаполнено(Репозиторий.Лицензия), Репозиторий.Лицензия, "Нет")
		        , Формат(Репозиторий.ДатаСоздания, "ДЛФ=DD")
		        , Репозиторий.Описание);
				
			Тексты.Добавить(Текст);
				
		КонецЦикла;
			
		Клавиатура = ПолучитьКлавиатуруРепозитория(Репозиторий.URL);
		Заголовок  = СтрШаблон("Новый проект - %1", Репозиторий.Наименование);
		
		СоздатьПост(Тексты, Клавиатура, Репозиторий, Заголовок);

    Исключение
        ИнструментарийВызовСервера.ЗаписатьИсключение(ОписаниеОшибки());
    КонецПопытки;
 
КонецПроцедуры

Процедура СоздатьПостНовогоРелиза(Знач Репозиторий, Знач Версия, Знач Дата) Экспорт
	
	Попытка
		
		Если НачалоДня(Репозиторий.ДатаДобавления) = НачалоДня(ТекущаяДатаСеанса()) Тогда
			Возврат;
		КонецЕсли;
			
		Автор       = Репозиторий.Автор.Наименование;
		Название    = Репозиторий.Наименование;
		
		URL       = СтрШаблон("https://github.com/%1/%2", Автор, Название);
		URLРелиза = СтрШаблон("%1/releases/tag/%2", URL, Версия);
		
		Тексты  = Новый Массив;
		Шаблоны = Новый Массив;
		Шаблоны.Добавить(ПолучитьОбщийМакет("СообщениеНовогоРелиза").ПолучитьТекст());
		Шаблоны.Добавить(ПолучитьОбщийМакет("НовостьНовогоРелиза").ПолучитьТекст());
		
		Для Каждого Шаблон Из Шаблоны Цикл
			
			Текст = СтрШаблон(Шаблон, Автор, Название, URL, Версия, Формат(Дата, "ДЛФ=DD"));	
			Тексты.Добавить(Текст);
				
		КонецЦикла;
		
		Клавиатура = ПолучитьКлавиатуруРелиза(URLРелиза);
		Заголовок  = СтрШаблон("Новый релиз %1 - %2", Репозиторий.Наименование, Версия);
		СоздатьПост(Тексты, Клавиатура, Репозиторий, Заголовок);
		
	Исключение
		ИнструментарийВызовСервера.ЗаписатьИсключение(ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

Процедура ОпубликоватьПодборку() Экспорт
	
	Попытка
		
		Подборка = ПолучитьПоследнююПодборку();
		
		Если Подборка = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ЭлементыПодборки = ПолучитьЭлементыПодборки(Подборка);
		ТекстПодборки    = ПолучитьТекстПодборки(Подборка, ЭлементыПодборки);
		
		Токен    = ИнструментарийВызовСервера.ПолучитьНастройку("ТокенТелеграм");
	    Чат      = ИнструментарийВызовСервера.ПолучитьНастройку("ЧатТелеграм"); 
        Картинка = ИнструментарийВызовСервера.ПолучитьФайл("Подборка");
		
		Ответ = OPI_Telegram.ОтправитьКартинку(Токен, Чат, ТекстПодборки, Картинка);
		
		Если Ответ["ok"] Тогда
				
    		МЗаписи = РегистрыСведений.ПубликацииПодборок.СоздатьМенеджерЗаписи();
    		МЗаписи.Подборка = Подборка;
    		МЗаписи.Период = ТекущаяДатаСеанса();
    		МЗаписи.Записать(Истина);
    		
		Иначе
            Ошибка = OPI_Инструменты.JSONСтрокой(Ответ);
            ИнструментарийВызовСервера.ЗаписатьИсключение(Ошибка);
		КонецЕсли;
		
    Исключение
        ИнструментарийВызовСервера.ЗаписатьИсключение(ОписаниеОшибки());
    КонецПопытки;
		
КонецПроцедуры

Процедура ОпубликоватьЕженедельныйТоп() Экспорт
    
    Запрос = Новый Запрос;
    Запрос.Текст = 
        "ВЫБРАТЬ ПЕРВЫЕ 5
        |   Тренды.Репозиторий.Автор.Наименование + ""/"" + Тренды.Репозиторий.Наименование КАК Репозиторий,
        |   СУММА(Тренды.Изменение) КАК Изменение,
        |   СтатистикаРепозиториев.Звезды КАК Звезды,
        |   Тренды.Репозиторий.URL КАК URL
        |ИЗ
        |   РегистрСведений.Тренды КАК Тренды
        |       ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатистикаРепозиториев КАК СтатистикаРепозиториев
        |       ПО Тренды.Репозиторий = СтатистикаРепозиториев.Репозиторий
        |ГДЕ
        |   Тренды.Период МЕЖДУ &Дата0 И &Дата1
        |   И НЕ Тренды.Репозиторий.ПометкаУдаления
        |
        |СГРУППИРОВАТЬ ПО
        |   СтатистикаРепозиториев.Звезды,
        |   Тренды.Репозиторий.Автор.Наименование + ""/"" + Тренды.Репозиторий.Наименование,
        |   Тренды.Репозиторий.URL
        |
        |УПОРЯДОЧИТЬ ПО
        |   Изменение УБЫВ";
    
    
    ТДС = ТекущаяДатаСеанса();
    
    Запрос.УстановитьПараметр("Дата0", ТДС - 60 * 60 * 24 * 7);
    Запрос.УстановитьПараметр("Дата1", ТДС);
    
    РезультатЗапроса = Запрос.Выполнить();
    
    ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
    
    Шаблон = "Репозиторий [%1](%2)
		|*+%3* %%E2%%AD%%90 (%4 всего)
		|";
	
    Текст  = "%F0%9F%8F%86 *Самые трендовые репозитории этой недели!*";
    Текст  = Текст + Символы.ПС + Символы.ПС;
    
    Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Изменение = Строка(ВыборкаДетальныеЗаписи.Изменение);
		
        Текст = Текст
            + СтрШаблон(Шаблон
                , ВыборкаДетальныеЗаписи.Репозиторий
                , ВыборкаДетальныеЗаписи.URL
                , ?(СтрДлина(ВыборкаДетальныеЗаписи.Изменение) = 1, " " + Изменение, Изменение)
                , ВыборкаДетальныеЗаписи.Звезды);
                
        Текст = Текст + Символы.ПС;
        
    КонецЦикла;
    
    Текст = Текст + Символы.ПС;
    Текст = Текст + "_Не забывайте поддерживать понравившиеся репозитории - от этого зависит их дальнейшее развитие!_";
           
    Токен    = ИнструментарийВызовСервера.ПолучитьНастройку("ТокенТелеграм");
    Чат      = ИнструментарийВызовСервера.ПолучитьНастройку("ЧатТелеграм");

    Картинка = ИнструментарийВызовСервера.ПолучитьФайл("Тренды");
	
	Клавиатура = ПолучитьКлавиатуруТрендов();
    Ответ = OPI_Telegram.ОтправитьКартинку(Токен, Чат, Текст, Картинка, Клавиатура);

    Если Не Ответ["ok"] Тогда
        Ошибка = OPI_Инструменты.JSONСтрокой(Ответ);
        ИнструментарийВызовСервера.ЗаписатьИсключение(Ошибка);
    КонецЕсли;	
    
КонецПроцедуры

Процедура ОпубликоватьРекламуБусти() Экспорт

	Текст = ПолучитьОбщийМакет("СообщениеРекламыБусти").ПолучитьТекст();
           
    Токен    = ИнструментарийВызовСервера.ПолучитьНастройку("ТокенТелеграм");
    Чат      = ИнструментарийВызовСервера.ПолучитьНастройку("ЧатТелеграм");

    Картинка = ИнструментарийВызовСервера.ПолучитьФайл("Бусти");
	
	Клавиатура = ПолучитьКлавиатуруБусти();
    Ответ = OPI_Telegram.ОтправитьКартинку(Токен, Чат, Текст, Картинка, Клавиатура);

    Если Не Ответ["ok"] Тогда
        Ошибка = OPI_Инструменты.JSONСтрокой(Ответ);
        ИнструментарийВызовСервера.ЗаписатьИсключение(Ошибка);
    КонецЕсли;  
    
КонецПроцедуры

Процедура ОпубликоватьБрошюру() Экспорт
    
    Текст = ПолучитьОбщийМакет("СообщениеБрошюры").ПолучитьТекст();       
        
    Токен    = ИнструментарийВызовСервера.ПолучитьНастройку("ТокенТелеграм");
    Чат      = ИнструментарийВызовСервера.ПолучитьНастройку("ЧатТелеграм");

    Картинка = ИнструментарийВызовСервера.ПолучитьФайл("Брошюра");
	
    Ответ = OPI_Telegram.ОтправитьКартинку(Токен, Чат, Текст, Картинка, , "MarkdownV2");

    Если Не Ответ["ok"] Тогда
        Ошибка = OPI_Инструменты.JSONСтрокой(Ответ);
        ИнструментарийВызовСервера.ЗаписатьИсключение(Ошибка);
    КонецЕсли;  
    
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьКлавиатуруТрендов()

    Строки = Новый Массив;

    Кнопки = Новый Массив;
    Кнопки.Добавить(Новый Структура("text,url", РаскодироватьСтроку("%F0%9F%93%88 Перейти в таблицу рейтинга", СпособКодированияСтроки.URLВКодировкеURL), "https://openyellow.org/grid?data=top"));
    Строки.Добавить(Кнопки);

    СтруктураПараметра = Новый Структура("inline_keyboard,rows", Строки, 1);

    Клавиатура = OPI_Инструменты.JSONСтрокой(СтруктураПараметра);

    Возврат Клавиатура;
    
КонецФункции

Функция ПолучитьКлавиатуруБусти()

    Строки = Новый Массив;

    Кнопки = Новый Массив;
    Кнопки.Добавить(Новый Структура("text,url", РаскодироватьСтроку("%E2%AD%90 Перейти на Boosty", СпособКодированияСтроки.URLВКодировкеURL), "https://boosty.to/bayselonarrend"));
    Строки.Добавить(Кнопки);

    СтруктураПараметра = Новый Структура("inline_keyboard,rows", Строки, 1);

    Клавиатура = OPI_Инструменты.JSONСтрокой(СтруктураПараметра);

    Возврат Клавиатура;
    
КонецФункции


Функция ПолучитьКлавиатуруРепозитория(URLРепозитория)

    Строки = Новый Массив;

    Кнопки = Новый Массив;
	Текст  = РаскодироватьСтроку("%F0%9F%8C%8F Перейти к репозиторию", СпособКодированияСтроки.URLВКодировкеURL);
	
    Кнопки.Добавить(Новый Структура("text,url", Текст, URLРепозитория));
    Строки.Добавить(Кнопки);

    СтруктураПараметра = Новый Структура("inline_keyboard,rows", Строки, 1);

    Клавиатура = OPI_Инструменты.JSONСтрокой(СтруктураПараметра);

    Возврат Клавиатура;
    
КонецФункции

Функция ПолучитьКлавиатуруРелиза(URLРелиза)
	
	Строки = Новый Массив;
	
	Кнопки = Новый Массив;
    Кнопки.Добавить(Новый Структура("text,url", РаскодироватьСтроку("%F0%9F%92%AB Перейти к релизу", СпособКодированияСтроки.URLВКодировкеURL), URLРелиза));
    Строки.Добавить(Кнопки);

    СтруктураПараметра = Новый Структура("inline_keyboard,rows", Строки, 1);

    Клавиатура = OPI_Инструменты.JSONСтрокой(СтруктураПараметра);

    Возврат Клавиатура;

КонецФункции

Функция ПолучитьПоследнююПодборку()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РепозиторииПодборки.Подборка КАК Подборка,
		|	СУММА(1) КАК Количество
		|ПОМЕСТИТЬ Подборки
		|ИЗ
		|	Справочник.Репозитории.Подборки КАК РепозиторииПодборки
		|
		|СГРУППИРОВАТЬ ПО
		|	РепозиторииПодборки.Подборка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	Подборки.Подборка КАК Подборка,
		|	МАКСИМУМ(ЕСТЬNULL(ПубликацииПодборокСрезПоследних.Период, ДАТАВРЕМЯ(1, 1, 1))) КАК Поле1
		|ИЗ
		|	Подборки КАК Подборки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПубликацииПодборок.СрезПоследних КАК ПубликацииПодборокСрезПоследних
		|		ПО Подборки.Подборка = ПубликацииПодборокСрезПоследних.Подборка
		|ГДЕ
		|	Подборки.Количество > 4
		|
		|СГРУППИРОВАТЬ ПО
		|	Подборки.Подборка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Поле1";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.Подборка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции

Функция ПолучитьЭлементыПодборки(Подборка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РепозиторииПодборки.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Репозитории.Подборки КАК РепозиторииПодборки
		|ГДЕ
		|	РепозиторииПодборки.Подборка = &Подборка";
	
	Запрос.УстановитьПараметр("Подборка", Подборка);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	СписокЭлементов  = Новый СписокЗначений;
	
	ГСЧ = Новый ГенераторСлучайныхЧисел();
	
	Пока СписокЭлементов.Количество() < 5 Цикл
		
		СлучайныйЭлемент = ГСЧ.СлучайноеЧисло(0, РезультатЗапроса.Количество() - 1); 
		СлучайныйЭлемент = РезультатЗапроса[СлучайныйЭлемент]["Ссылка"];
		
		Если СписокЭлементов.НайтиПоЗначению(СлучайныйЭлемент) = Неопределено Тогда
			СписокЭлементов.Добавить(СлучайныйЭлемент);
		Иначе
			Продолжить;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СписокЭлементов.ВыгрузитьЗначения();

КонецФункции

Функция ПолучитьТекстПодборки(Подборка, ЭлементыПодборки)
	
	ШаблонПодборки = "%%F0%%9F%%9A%%80 *Подборка репозиториев по теме ""%1"" *
	|%2
	|%3
	|
	|_Не забывайте ставить %%E2%%AD%%90 понравившимся проектам_";
	
	ШаблонЭлемента = "
	|
	|%%E2%%96%%B6 [%1/%2](%3)
	|%4";
	
	ТекстЭлементов = "";
	
	Для Каждого ЭлементПодборки Из ЭлементыПодборки Цикл
		ТекстЭлементов = ТекстЭлементов + СтрШаблон(ШаблонЭлемента
			, ЭлементПодборки.Автор
			, ЭлементПодборки.Наименование
			, ЭлементПодборки.URL
			, ЭлементПодборки.Описание);
	КонецЦикла;
		
	ТекстПодборки = СтрШаблон(ШаблонПодборки
		, Подборка.Наименование
		, Подборка.Описание
		, ТекстЭлементов);
		
	Возврат ТекстПодборки;
	
КонецФункции

Функция СоздатьПост(Текст, Клавиатура, Репозиторий, Заголовок = "")
	
	Если ТипЗнч(Текст) = Тип("Массив") Тогда
		ТекстПоста   = Текст[0];
		ТекстНовости = Текст[1];
	Иначе
		ТекстПоста   = Текст;
		ТекстНовости = "";
	КонецЕсли;
	
	НовыйПост = Справочники.Посты.СоздатьЭлемент();
	НовыйПост.Текст       = ТекстПоста;
	НовыйПост.Новость     = ТекстНовости;
	НовыйПост.Заголовок   = Заголовок;
	НовыйПост.Дата        = ТекущаяДатаСеанса();
	НовыйПост.Клавиатура  = Клавиатура;
	НовыйПост.Статус      = Перечисления.СогласованиеПостов.Новый;
	НовыйПост.Репозиторий = Репозиторий.Ссылка;
	НовыйПост.URL         = НовыйПост.Репозиторий.URL;

	НовыйПост.Записать();
	
	МЗаписи = РегистрыСведений.ОчередьВыгрузки.СоздатьМенеджерЗаписи();
	МЗаписи.ОбъектВыгрузки = НовыйПост.Ссылка;
	МЗаписи.Записать(Истина);
	
	Возврат НовыйПост.Код;

КонецФункции

#КонецОбласти