// Copyright (c) 2023-2025 Anton Tsitavets

// 
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
// 
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
// 
// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <https://www.gnu.org/licenses/>.

// https://github.com/OpenBSL/OpenYellow

//@skip-check undefined-variable
//@skip-check bsl-legacy-check-string-literal

#Область СлужебныйПрограммныйИнтерфейс

Процедура ОбработатьСобытия() Экспорт
	
	Токен            = ИнструментарийВызовСервера.ПолучитьНастройку("ТокенТелеграм");
	ПоследнееСобытие = ИнструментарийВызовСервера.ПолучитьНастройку("ПоследнееСобытие");

	Обновления = OPI_Telegram.ПолучитьОбновления(Токен, 55, ПоследнееСобытие + 1);
	Результат  = Обновления["result"];
    Обработано = Ложь;
    Смещение   = Новый Соответствие;
	
	Для Каждого Элемент Из Результат Цикл
		
		ПоследнееСобытие = Элемент["update_id"];
		
		Обработано = ОбработатьСобытиеСозданияОбсуждения(Токен, Элемент);
        
        Если Обработано Тогда Продолжить; КонецЕсли;
        
        //@skip-check query-in-loop
        Обработано = ОбработатьСобытиеНовогоКомментария(Токен, Элемент, Смещение);
        
		Если Обработано Тогда Продолжить; КонецЕсли;
		
	КонецЦикла;
	
	ИнструментарийВызовСервера.УстановитьНастройку("ПоследнееСобытие", ПоследнееСобытие);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОбработатьСобытиеСозданияОбсуждения(Знач Токен, Знач Событие)
	
	Попытка
		
		Если Не OPI_Инструменты.ПолеКоллекцииСуществует(Событие, "message") Тогда
			Возврат Ложь;
		КонецЕсли;

		СообщениеЭлемента = Событие["message"];

		Если Не OPI_Инструменты.ПолеКоллекцииСуществует(СообщениеЭлемента, "forward_from_message_id")
			Или Не OPI_Инструменты.ПолеКоллекцииСуществует(СообщениеЭлемента, "is_automatic_forward") Тогда
			Возврат Ложь;
		КонецЕсли;

		Если Не СообщениеЭлемента["is_automatic_forward"] Тогда
			Возврат Ложь;
		КонецЕсли;

		ОригинальноеСообщение = СообщениеЭлемента["forward_from_message_id"];
		ПересланноеСообщение  = СообщениеЭлемента["message_id"];
		
		Пост = Справочники.Посты.НайтиПоРеквизиту("IDПоста", ОригинальноеСообщение);
		
		Если Не ЗначениеЗаполнено(Пост) Тогда
			ПостОбъект = Справочники.Посты.СоздатьЭлемент();
			ПостОбъект.IDОбсуждения = ПересланноеСообщение;
			ПостОбъект.IDПоста = ОригинальноеСообщение;
			ПостОбъект.Текст = "<Введен вручную>";
			ПостОбъект.Клавиатура = "{""inline_keyboard"":[[]],""rows"":1}";
			ПостОбъект.Статус = Перечисления.СогласованиеПостов.Опубликован;
			ПостОбъект.Дата = ТекущаяДатаСеанса();
			ПостОбъект.Записать();
			Пост = ПостОбъект.Ссылка;
		Иначе
			ПостОбъект = Пост.ПолучитьОбъект();
			ПостОбъект.IDОбсуждения = ПересланноеСообщение;
			ПостОбъект.Записать();
		КонецЕсли;
		
		Чат          = -СообщениеЭлемента["chat"]["id"] - 1000000000000;
		ЧатОригинала = СообщениеЭлемента["sender_chat"]["id"];

		Если OPI_Инструменты.ПолеКоллекцииСуществует(СообщениеЭлемента, "reply_markup") Тогда
			Клавиатура = СообщениеЭлемента["reply_markup"];
		Иначе
			Клавиатура = Новый Структура("inline_keyboard", Новый Массив);
		КонецЕсли;

		ШаблонURL = "https://t.me/c/%1/%2?thread=%2";
		URL       = СтрШаблон(ШаблонURL, Чат, ПересланноеСообщение);

		Линия  = Новый Массив;
		Текст  = РаскодироватьСтроку("%F0%9F%92%AC Перейти к обсуждению (0)", СпособКодированияСтроки.URLВКодировкеURL);
		Кнопка = Новый Структура("url,text", URL, Текст);
		Линия.Добавить(Кнопка);

		Клавиатура["inline_keyboard"].Добавить(Линия);

		Ответ = OPI_Telegram.ЗаменитьКлавиатуруСообщения(Токен, ЧатОригинала, ОригинальноеСообщение, Клавиатура);
		
		Если Не Ответ["ok"] Тогда
            Ошибка = OPI_Инструменты.JSONСтрокой(Ответ);
            ИнструментарийВызовСервера.ЗаписатьИсключение(Ошибка);
	    КонецЕсли;  
		    
		Возврат Истина;
        
	Исключение
		
		ИнструментарийВызовСервера.ЗаписатьИсключение(ОписаниеОшибки());
        Возврат Ложь;
        
	КонецПопытки
	
КонецФункции

Функция ОбработатьСобытиеНовогоКомментария(Знач Токен, Знач Событие, Смещение)
    
    Попытка
        
        Если Не OPI_Инструменты.ПолеКоллекцииСуществует(Событие, "message") Тогда
            Возврат Ложь;
        КонецЕсли;
        
        СообщениеЭлемента = Событие["message"];
        
        Если Не OPI_Инструменты.ПолеКоллекцииСуществует(СообщениеЭлемента, "reply_to_message") Тогда
			Возврат Ложь;
        КонецЕсли;
        
        ЭлементОсновногоЧата = СообщениеЭлемента["reply_to_message"];
		Пост                 = Неопределено;
        
        Если Не OPI_Инструменты.ПолеКоллекцииСуществует(ЭлементОсновногоЧата, "forward_from_chat")
            Или Не OPI_Инструменты.ПолеКоллекцииСуществует(ЭлементОсновногоЧата, "forward_from_message_id")
            Или Не OPI_Инструменты.ПолеКоллекцииСуществует(ЭлементОсновногоЧата, "reply_markup") Тогда
			
            IDОсновногоСообщения = ЭлементОсновногоЧата["message_id"];
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ ПЕРВЫЕ 1
				|	ПостыКомментарии.Ссылка КАК Пост
				|ИЗ
				|	Справочник.Посты.Комментарии КАК ПостыКомментарии
				|ГДЕ
				|	ПостыКомментарии.IDКомментария = &IDКомментария";
			
			Запрос.УстановитьПараметр("IDКомментария", IDОсновногоСообщения);
			
			РезультатЗапроса = Запрос.Выполнить();
			
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			
			Если ВыборкаДетальныеЗаписи.Следующий() Тогда
				Пост = ВыборкаДетальныеЗаписи.Пост;
			КонецЕсли;
			
		Иначе
			
        	ОригинальноеСообщение = ЭлементОсновногоЧата["forward_from_message_id"];	
			Пост = Справочники.Посты.НайтиПоРеквизиту("IDПоста", ОригинальноеСообщение);
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Пост) Тогда
			Возврат Ложь;
		КонецЕсли;
		
		ПостОбъект       = Пост.ПолучитьОбъект();
		
		НовыйКомментарий = ПостОбъект.Комментарии.Добавить();
		НовыйКомментарий.IDКомментария = СообщениеЭлемента["message_id"];
		
		ПостОбъект.Записать();
		
		ЧислоКомментариев = ПостОбъект.Комментарии.Количество();
		
        Клавиатура = ПостОбъект.Клавиатура;
		OPI_ПреобразованиеТипов.ПолучитьКоллекцию(Клавиатура);
		
		Если Не OPI_Инструменты.ПолеКоллекцииСуществует(Клавиатура, "inline_keyboard") Тогда
            Возврат Ложь;
        КонецЕсли;
		
		ЧатОсновной   = ИнструментарийВызовСервера.ПолучитьНастройку("ЧатТелеграм");
		ЧатОбсуждения = -СообщениеЭлемента["chat"]["id"] - 1000000000000;
		
		IDПоста      = ПостОбъект.IDПоста;
		IDОбсуждения = ПостОбъект.IDОбсуждения;

		ШаблонURL = "https://t.me/c/%1/%2?thread=%2";
		URL       = СтрШаблон(ШаблонURL, ЧатОбсуждения, IDОбсуждения);

		Линия  = Новый Массив;
		Текст  = СтрШаблон("%%F0%%9F%%92%%AC Перейти к обсуждению (%1)", ЧислоКомментариев);
		Текст  = РаскодироватьСтроку(Текст, СпособКодированияСтроки.URLВКодировкеURL);
		Кнопка = Новый Структура("url,text", URL, Текст);
		Линия.Добавить(Кнопка);

		Клавиатура["inline_keyboard"].Добавить(Линия);
  
        Ответ = OPI_Telegram.ЗаменитьКлавиатуруСообщения(Токен, ЧатОсновной, IDПоста, Клавиатура);

        Если Не Ответ["ok"] Тогда
            Ошибка = OPI_Инструменты.JSONСтрокой(Ответ);
            ИнструментарийВызовСервера.ЗаписатьИсключение(Ошибка);
        КонецЕсли;  
            
        Возврат Истина;
        
    Исключение
        
        ИнструментарийВызовСервера.ЗаписатьИсключение(ОписаниеОшибки());
        Возврат Ложь;
        
    КонецПопытки

КонецФункции

#КонецОбласти
