#Область ОписаниеПеременных

&НаКлиенте
Перем Расписание;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Для Каждого Метаданное из Метаданные.РегламентныеЗадания Цикл
		Элементы.МетаданныеВыбор.СписокВыбора.Добавить(Метаданное.Имя, Метаданное.Представление());
	КонецЦикла;
	
	Попытка
		ПользователиИБ = ПользователиИнформационнойБазы.ПолучитьПользователей();
	Исключение
		//@skip-check use-non-recommended-method
		Сообщить("Ошибка при получении списка пользователей информационной базы: " + ОписаниеОшибки());
		ПользователиИБ = Неопределено;
	КонецПопытки;
	
	Если ПользователиИБ <> Неопределено Тогда
		
		Для Каждого Пользователь из ПользователиИБ Цикл
			Элементы.ПользователиВыбор.СписокВыбора.Добавить(Пользователь.Имя, Пользователь.ПолноеИмя);
		КонецЦикла;
	
	КонецЕсли;

	мВозможностьИзменятьМетаданные = Истина;
	
	//@skip-check unknown-form-parameter-access
	//@skip-check unknown-form-parameter-access
	РегламентноеЗаданиеИД = Параметры.ИдентификаторЗадания;
	РегламентноеЗадание = РеквизитФормыВЗначение("ОбработкаОбъект").ПолучитьОбъектРегламентногоЗадания(РегламентноеЗаданиеИД);
	Если РегламентноеЗадание <> Неопределено Тогда
		
		МетаданныеВыбор = РегламентноеЗадание.Метаданные.Имя;
		
		мВозможностьИзменятьМетаданные = Ложь;
		Наименование = РегламентноеЗадание.Наименование;
		Ключ = РегламентноеЗадание.Ключ;
		Использование = РегламентноеЗадание.Использование;
		ПользователиВыбор = РегламентноеЗадание.ИмяПользователя;
		КоличествоПовторовПриАварийномЗавершении = РегламентноеЗадание.КоличествоПовторовПриАварийномЗавершении;
		ИнтервалПовтораПриАварийномЗавершении = РегламентноеЗадание.ИнтервалПовтораПриАварийномЗавершении;
		
		Расписание = РегламентноеЗадание.Расписание;
		
		// Добавлены параметры
		Для Каждого Параметр Из РегламентноеЗадание.Параметры Цикл
			НоваяСтрока = ПараметрыЗадания.Добавить();
			НоваяСтрока.НомерСтроки = ПараметрыЗадания.Индекс(НоваяСтрока)+1;
			НоваяСтрока.Значение = Параметр;
			МассивТипов = Новый Массив;
			МассивТипов.Добавить(ТипЗнч(Параметр));
			НоваяСтрока.Тип = Новый ОписаниеТипов(МассивТипов);
		КонецЦикла;
		
	Иначе
		Расписание = Новый РасписаниеРегламентногоЗадания;
	КонецЕсли;
	
	Элементы.МетаданныеВыбор.Доступность = мВозможностьИзменятьМетаданные;
	Элементы.НадписьРасписание.Заголовок = "Выполнять: " + Строка(Расписание);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Расписание = ПолучитьРасписаниеРегламентногоЗадания(РегламентноеЗаданиеИД);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИзменитьРасписаниеНажатие(Элемент)
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(Расписание);
	
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ДиалогРасписанияРегламентногоЗаданияОткрытьЗавершение", ЭтотОбъект);
	
	Диалог.Показать(ОписаниеОповещенияОЗакрытии);
КонецПроцедуры

&НаКлиенте
Процедура МетаданныеВыборОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Наименование = Элемент.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение).Представление;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	Если ЗаписатьРегламентноеЗадание(Расписание) Тогда
		Закрыть(РегламентноеЗаданиеИД);
	КонецЕсли;
КонецПроцедуры

#Если Не ВебКлиент Тогда
&НаКлиенте
Процедура УстановитьУникальныйКлюч(Команда)

	Если ЗначениеЗаполнено(Ключ) Тогда
		Ответ = Вопрос("Ключ рег. задания заполнен!Перезаполнить уникальным значением?",РежимДиалогаВопрос.ДаНет,10,,"Предупреждение!");
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли; 
	КонецЕсли; 
	
	
	Если Не ЗначениеЗаполнено(Ключ) Тогда
		СформироватьУникальныйКлючРекурсивно(Строка(Новый УникальныйИдентификатор),РегламентноеЗаданиеИД);
	Иначе
		СформироватьУникальныйКлючРекурсивно(Ключ,РегламентноеЗаданиеИД);
	КонецЕсли; 
 	
КонецПроцедуры
#КонецЕсли

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ДиалогРасписанияРегламентногоЗаданияОткрытьЗавершение(РасписаниеРезультат, ДополнительныеПараметры) Экспорт
	Если РасписаниеРезультат <> Неопределено Тогда
		Расписание = РасписаниеРезультат;
		Элементы.НадписьРасписание.Заголовок = "Выполнять: " + Строка(Расписание);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ЗаписатьРегламентноеЗадание(Расписание)
	Попытка
		
		Если МетаданныеВыбор = Неопределено ИЛИ МетаданныеВыбор = "" Тогда
			ВызватьИсключение("Не выбраны метаданные регламентного задания.");
		КонецЕсли;
		
		РегламентноеЗадание = РеквизитФормыВЗначение("ОбработкаОбъект").ПолучитьОбъектРегламентногоЗадания(РегламентноеЗаданиеИД);
		
		Если РегламентноеЗадание = Неопределено Тогда
			РегламентноеЗадание = РегламентныеЗадания.СоздатьРегламентноеЗадание(МетаданныеВыбор);
			РегламентноеЗаданиеИД = РегламентноеЗадание.УникальныйИдентификатор;
		КонецЕсли;
		
		РегламентноеЗадание.Наименование = Наименование;
		РегламентноеЗадание.Ключ = Ключ;
		РегламентноеЗадание.Использование = Использование;
		РегламентноеЗадание.ИмяПользователя = ПользователиВыбор;
		РегламентноеЗадание.КоличествоПовторовПриАварийномЗавершении = КоличествоПовторовПриАварийномЗавершении;
		РегламентноеЗадание.ИнтервалПовтораПриАварийномЗавершении = ИнтервалПовтораПриАварийномЗавершении;
		РегламентноеЗадание.Расписание = Расписание;
		
		// Добавлены параметры регламентного задания
		Если ПараметрыЗадания.Количество() Тогда
			РегламентноеЗадание.Параметры = ПараметрыЗадания.Выгрузить().ВыгрузитьКолонку("Значение");
		Иначе
			РегламентноеЗадание.Параметры = Новый Массив;
		КонецЕсли;
		
		РегламентноеЗадание.Записать();
	Исключение	
		
		//@skip-check use-non-recommended-method
		Сообщить("Ошибка: " + ОписаниеОшибки(), СтатусСообщения.Внимание);
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
КонецФункции

&НаСервере
Функция ПолучитьРасписаниеРегламентногоЗадания(УникальныйНомерЗадания)
	ОбъектЗадания = РеквизитФормыВЗначение("ОбработкаОбъект").ПолучитьОбъектРегламентногоЗадания(УникальныйНомерЗадания);
	Если ОбъектЗадания = Неопределено Тогда
		Возврат Новый РасписаниеРегламентногоЗадания;
	КонецЕсли;
	
	Возврат ОбъектЗадания.Расписание;
КонецФункции

&НаКлиенте
Процедура СформироватьУникальныйКлючРекурсивно(ПараметрКлюч,ИдРегламентногоЗалания)
	
	Если Не КлючРегламентногоЗаданияУникальный(ПараметрКлюч,ИдРегламентногоЗалания) Тогда
 		СформироватьУникальныйКлючРекурсивно(Строка(Новый УникальныйИдентификатор),ИдРегламентногоЗалания);
	Иначе
		Ключ = ПараметрКлюч;
	КонецЕсли; 
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция КлючРегламентногоЗаданияУникальный(Ключ,ИдРегламентногоЗалания)
	
 	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Ключ", Ключ );
	МассивРеглЗаданий = РегламентныеЗадания.ПолучитьРегламентныеЗадания(СтруктураОтбора);
	Если МассивРеглЗаданий.Количество() = 0 Тогда
		Возврат Истина;
	КонецЕсли; 
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти



