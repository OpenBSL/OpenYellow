//@skip-check Undefined variable

Процедура ОпубликоватьНовыйРепозиторий(Репозиторий) Экспорт
    
    Токен = Константы.ТокенТелеграм.Получить();
    Чат   = Константы.ЧатТелеграм.Получить();
    Тема  = Справочники.ТемыТелеграм.Продвижение.ID;
    
    Чат = Чат + "*" + Тема;
    
    Шаблон = "Новый проект от %1 - [%2](%3)!
    |
    |Язык: %4
    |Лицензия: %5
    |Ссылка: %6
    |";
    
    Текст = СтрШаблон(Шаблон
        , Репозиторий.Автор.Наименование
        , Репозиторий.Наименование
        , Репозиторий.URL
        , ?(ЗначениеЗаполнено(Репозиторий.Язык), Репозиторий.Язык, "другое")
        , ?(ЗначениеЗаполнено(Репозиторий.Лицензия), Репозиторий.Лицензия, "нет")
        , Репозиторий.URL);
        
    //Текст = СтрЗаменить(Текст, "(", "\(");
    //Текст = СтрЗаменить(Текст, ")", "\)");
    Текст = СтрЗаменить(Текст, "-", "\-");
    Текст = СтрЗаменить(Текст, "!", "\!");
    Текст = СтрЗаменить(Текст, ".", "\.");
    Текст = СтрЗаменить(Текст, "_", "\_");
    Текст = СтрЗаменить(Текст, "#", "\#");
    
    Ответ = OPI_Telegram.ОтправитьТекстовоеСообщение(Токен, Чат, Текст, , "MarkdownV2");
     
КонецПроцедуры

Процедура ОпубликоватьИзменения() Экспорт
    
    Токен = Константы.ТокенТелеграм.Получить();
    Чат   = Константы.ЧатТелеграм.Получить();
    Тема  = Справочники.ТемыТелеграм.Динамика.ID;

    Запрос = Новый Запрос;
    Запрос.Текст = 
        "ВЫБРАТЬ ПЕРВЫЕ 500
        |   СтатистикаРепозиториев.Репозиторий КАК Репозиторий,
        |   СтатистикаРепозиториев.ПрошлоеМесто КАК ПрошлоеМесто
        |ПОМЕСТИТЬ Отбор
        |ИЗ
        |   РегистрСведений.СтатистикаРепозиториев КАК СтатистикаРепозиториев
        |
        |УПОРЯДОЧИТЬ ПО
        |   СтатистикаРепозиториев.Звезды УБЫВ,
        |   СтатистикаРепозиториев.Форки УБЫВ,
        |   СтатистикаРепозиториев.Репозиторий.ДатаСоздания
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |   Отбор.Репозиторий КАК Репозиторий,
        |   Отбор.ПрошлоеМесто КАК ПрошлоеМесто,
        |   АВТОНОМЕРЗАПИСИ() КАК ТекущееМесто
        |ПОМЕСТИТЬ Нумерация
        |ИЗ
        |   Отбор КАК Отбор
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |   Нумерация.Репозиторий.Автор.Наименование + ""/"" + Нумерация.Репозиторий.Наименование КАК Репозиторий,
        |   Нумерация.Репозиторий.URL КАК URL,
        |   Нумерация.ТекущееМесто КАК ТекущееМесто,
        |   Нумерация.ПрошлоеМесто - Нумерация.ТекущееМесто КАК Изменение
        |ИЗ
        |   Нумерация КАК Нумерация
        |ГДЕ
        |   Нумерация.ТекущееМесто < Нумерация.ПрошлоеМесто";
    
    РезультатЗапроса = Запрос.Выполнить();
    
    Если РезультатЗапроса.Пустой() Тогда
        Возврат;
    КонецЕсли;
    
    ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
    МакетОдного = Символы.ПС + Символы.ПС + "[%1](%2) %%F0%%9F%%94%%BC %3 /(/#%4/)";
    Текст  = "Динамика изменения ТОП-500 за сегодня!"; 
    
    Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
        Текст = Текст 
            + СтрШаблон(МакетОдного
                , ВыборкаДетальныеЗаписи.Репозиторий
                , ВыборкаДетальныеЗаписи.URL
                , ВыборкаДетальныеЗаписи.Изменение
                , ВыборкаДетальныеЗаписи.ТекущееМесто);
    КонецЦикла;
            
    Чат = Чат + "*" + Тема;
    
    Текст = СтрЗаменить(Текст, "-", "\-");
    Текст = СтрЗаменить(Текст, "!", "\!");
    Текст = СтрЗаменить(Текст, ".", "\.");
    Текст = СтрЗаменить(Текст, "_", "\_");
    Текст = СтрЗаменить(Текст, "#", "\#");

    Ответ = OPI_Telegram.ОтправитьТекстовоеСообщение(Токен, Чат, Текст, , "MarkdownV2");        
     
КонецПроцедуры